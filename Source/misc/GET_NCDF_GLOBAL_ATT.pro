;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_NCDF_GLOBAL_ATT
;*
;* PURPOSE:
;*      GET A GLOBAL ATTRIBUTE FROM A NETCDF FILE
;*
;* CALLING SEQUENCE:
;*      RES = GET_NCDF_GLOBAL_ATT(NCDF_FILENAME, GLOB_ATT_NAME)
;*
;* INPUTS:
;*      NCDF_FILENAME = A STRING CONTAINING THE FULL PATH OF THE NETCDF FILE 
;*      GLOB_ATT_NAME = A STRING CONTAINING THE NAME OF THE GLOBAL ATTRIBUTE TO GET
;*
;* KEYWORDS:
;*      VERBOSE  - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      STATUS   - 1: NOMINAL, (-1): ERROR, 0: NO PRODUCTS FOUND
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      17 FEB 2014 - PML / MAGELLIUM (MAG) - CREATION
;*
;* VALIDATION HISTORY:
;*      20 JAN 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL 
;*      30 MAR 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL (DIMITRI V4.0) 

;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_NCDF_GLOBAL_ATT, NCDF_FILENAME, GLOB_ATT_NAME, VERBOSE=VERBOSE

  STATUS_ERROR = GET_DIMITRI_LOCATION('STATUS_ERROR')

  NCDF_FILENAME_INFO = FILE_INFO(NCDF_FILENAME)
  IF NCDF_FILENAME_INFO.EXISTS EQ 0 THEN BEGIN
    PRINT, 'GET_NCDF_GLOBAL_ATT: NCDF FILE ' + NCDF_FILENAME + ' DO NOT EXIST'
    RETURN, STATUS_ERROR
  ENDIF
  
  FID = NCDF_OPEN(NCDF_FILENAME,/NOWRITE )
  IF FID EQ -1 THEN BEGIN
    PRINT, 'GET_NCDF_GLOBAL_ATT: NCDF FILE ' + NCDF_FILENAME + ' CAN NOT BE OPEN'
    RETURN, STATUS_ERROR
  ENDIF
  
  FINQ = NCDF_INQUIRE(FID)    ; FINQ /STR = NDIMS, NVARS, NGATTS, RECDIM
  IF N_TAGS(FINQ) LT 4 THEN BEGIN
    PRINT, 'GET_NCDF_GLOBAL_ATT: NCDF FILE ' + NCDF_FILENAME + ' CONTENT IS NOK OR NOT IN NCDF FILE'
    NCDF_CLOSE, FID
    RETURN, STATUS_ERROR
  ENDIF
  
  ; GET GLOBAL ATTRIBUTE
  NCDF_ATTGET, FID, GLOB_ATT_NAME, GLOB_ATT_VALUE, /GLOBAL
  
  ; CLOSE NETCDF FILE
  NCDF_CLOSE, FID
  
  ; RETURN GLOBAL ATTRIBUTE
  RETURN, STRING(GLOB_ATT_VALUE)
  
END