;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_SPECTRAL_RESPONSE_TEMPLATE       
;* 
;* PURPOSE:
;*      RETURNS THE TEMPLATE REQUIRED FOR READING THE SPECTRAL RESPONSE FILE
;* 
;* CALLING SEQUENCE:
;*      RES = GET_SPECTRAL_RESPONSE_TEMPLATE()      
;* 
;* INPUTS:
;*      SENSOR  - A STRING OF THE SENSOR NAME (E.G. 'MERIS')
;*
;* KEYWORDS:
;*      VERBOSE  - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      INDEX_TEMPLATE  - A TEMPLATE FOR READING THE SPECTRAL RESPONSE FILE
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      12 DEC 2014 - NCG / MAGELLIUM   - DIMITRI-3.0 MAG
;;*
;* VALIDATION HISTORY:
;*      20 JAN 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL 
;*      30 MAR 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL (DIMITRI V4.0) 
;*       
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_SPECTRAL_RESPONSE_TEMPLATE, SENSOR,VERBOSE=VERBOSE

	STATUS_ERROR = GET_DIMITRI_LOCATION('STATUS_ERROR')

	IF KEYWORD_SET(VERBOSE) THEN PRINT,'GET_SPECTRAL_RESPONSE_TEMPLATE: RETRIEVING TEMPLATE'

	CASE STRUPCASE(SENSOR) OF

		'AATSR'     : NB_BAND = 4
		'ATSR2'     : NB_BAND = 4
		'MERIS'     : NB_BAND = 15
		'MODISA'    : NB_BAND = 22
		'PARASOL'   : NB_BAND = 9
		'VEGETATION': NB_BAND = 4

		ELSE: BEGIN
			PRINT,'GET_SPECTRAL_RESPONSE_TEMPLATE: ERROR - SENSOR ' + SENSOR +' NOT IMPLEMENTED'
			RETURN, STATUS_ERROR
			END

	END

	TEMP_NAME = STRARR(NB_BAND + 1)
	TEMP_TYPE = INTARR(NB_BAND + 1)
	FIELDGROUPS = INTARR(NB_BAND + 1)

	TEMP_NAME[0] = 'SPECTRAL_RESPONSE_TAB'   
	FOR NUM = 1, NB_BAND DO TEMP_NAME[NUM] = 'BAND_' + STRTRIM(STRING(NUM,FORMAT='(%"%02d")'),2)

	TEMP_TYPE[*] = 4
	FIELDGROUPS[*] = 0 ; FIELDS ARE GROUPED IN A TABLE

	INDEX_TEMPLATE = {VERSION       : 1.0 , $
									 DATASTART      : 0 , $
									 DELIMITER      : ' ' , $
									 MISSINGVALUE   : !VALUES.F_NAN, $
									 COMMENTSYMBOL  : ''          , $
									 FIELDCOUNT     : N_ELEMENTS(TEMP_NAME), $
									 FIELDTYPES     : TEMP_TYPE, $
									 FIELDNAMES     : TEMP_NAME, $
									 FIELDLOCATIONS : INDGEN(N_ELEMENTS(TEMP_NAME)), $
									 FIELDGROUPS    : FIELDGROUPS} 

	;IF KEYWORD_SET(VERBOSE) THEN HELP,INDEX_TEMPLATE       
	RETURN, INDEX_TEMPLATE

END