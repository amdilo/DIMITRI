;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      DIMITRI_INTERFACE_INGEST       
;* 
;* PURPOSE:
;*      INTERFACES BETWEEN THE DIMITRI HMI AND THE PRODUCT INGESTION ROUTINES
;* 
;* CALLING SEQUENCE:
;*      RES = DIMITRI_INTERFACE_INGEST(INGEST_REGION,INGEST_SENSOR,INGEST_PROC_VER)      
;* 
;* INPUTS:
;*      INGEST_REGION = A STRING CONTAINING THE NAME OF THE REGION TO BE USED
;*      INGEST_SENSOR = A STRING CONTAINING THE NAME OF THE SENSOR TO BE USED
;*      INGEST_PROC_VER = A STRING CONTAINING THE NAME OF THE PROCESSING_VERSION TO BE USED
;*
;* KEYWORDS:
;*      ALL      - SET TO SEARCH FOR ALL DIMITRI PRODUCTS
;*      YEAR     - A STRING OF THE YEAR TO BE INGESTED (E.G. '2002')
;*      VERBOSE  - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      STATUS   - 1: NOMINAL, (-1): ERROR, 0: NO PRODUCTS FOUND
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      23 DEC 2010 - C KENT   - DIMITRI-2 V1.0
;*      05 JAN 2011 - C KENT   - ADDED COLOUR_TABLE AND PLOT SIZE KEYWORDS, UPDATED DELIMITER COMPUTATION
;*      21 MAR 2011 - C KENT   - MODIFIED FILE DEFINITION TO USE GET_DIMITRI_LOCATION
;*      22 MAR 2011 - C KENT   - ADDED CONFIGURAITON FILE DEPENDENCE
;*      23 MAR 2011 - C KENT   - ADDED MODISA LINUX PROCESSING
;*	    07 DEC 2011 - C KENT   - FIXED INGEST BUG FOR DUPLICATED FILENAMES
;*
;* VALIDATION HISTORY:
;*      23 DEC 2010 - C KENT   - WINDOWS 32-BIT MACHINE: COMPILATION SUCCESSFUL,
;*                               KEYWORD PARAMETERS CHECKED, NOMINAL EXECUTION
;*      05 JAN 2010 - C KENT   - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL,
;*                               NO DIFFERENCES OBSERVED AGAINST WINDOWS 32-BIT MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION DIMITRI_INTERFACE_INGEST,INGEST_REGION,INGEST_SENSOR,INGEST_PROC_VER,ALL=ALL,YEAR=YEAR,$
COLOUR_TABLE=COLOUR_TABLE,PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,ENDIAN_SIZE=ENDIAN_SIZE,VERBOSE=VERBOSE

;------------------------
; KEYWORD PARAMETER CHECK

  CFIG_DATA = GET_DIMITRI_CONFIGURATION(VERBOSE=VERBOSE)
  IF N_ELEMENTS(COLOUR_TABLE) EQ 0 THEN BEGIN
    PRINT, 'DIM_INTER INGEST: NO COLOR_TABLE SET, USING DEFAULT'
    COLOUR_TABLE = CFIG_DATA.(1)[6]
  ENDIF
  IF N_ELEMENTS(PLOT_XSIZE) EQ 0 THEN BEGIN
    PRINT, 'DIM_INTER INGEST: PLOT_XSIZE NOT SET, USING DEFAULT'
    PLOT_XSIZE = CFIG_DATA.(1)[0]
  ENDIF
  IF N_ELEMENTS(PLOT_YSIZE) EQ 0 THEN BEGIN
    PRINT, 'DIM_INTER INGEST: PLOT_YSIZE NOT SET, USING DEFAULT'
    PLOT_YSIZE = CFIG_DATA.(1)[1]
  ENDIF  
  IF N_ELEMENTS(ENDIAN_SIZE) EQ 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN BEGIN
      PRINT, 'DIM_INTER INGEST: NO ENDIAN SIZE PROVIDED, RETRIEVING...'
      ENDIAN_SIZE = GET_ENDIAN_SIZE(/VERBOSE)
    ENDIF ELSE ENDIAN_SIZE = GET_ENDIAN_SIZE()
  ENDIF

;----------------------
; IDENTIFIY CURRENT DIRECTORY AND INPUT FOLDER

  DELIM         = GET_DIMITRI_LOCATION('DL')
  INPUT_FOLDER  = GET_DIMITRI_LOCATION('INPUT')

  IF KEYWORD_SET(ALL) THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'DIM_INTER INGEST: SEARCHING FOR ALL PRODUCT TYPES'
    SEARCH_SENSOR = 'ALL'
    SEARCH_FOLDER = INPUT_FOLDER
  ENDIF ELSE BEGIN
    SEARCH_SENSOR = INGEST_SENSOR
    IF KEYWORD_SET(YEAR) THEN SEARCH_FOLDER = INPUT_FOLDER+DELIM+'Site_'+INGEST_REGION+DELIM+INGEST_SENSOR+DELIM+'Proc_'+INGEST_PROC_VER+DELIM+YEAR $
      ELSE SEARCH_FOLDER = INPUT_FOLDER+DELIM+'Site_'+INGEST_REGION+DELIM+INGEST_SENSOR+DELIM+'Proc_'+INGEST_PROC_VER
  ENDELSE

;---------------------
; RETRIEVE SENSOR PRODUCT SEARCH STRINGS

    if keyword_set(verbose) then PRINT, 'DIM_INTER INGEST: RETRIEVING PRODUCT SEARCH FILTER'
    SEARCH_FILTER = GET_PRODUCT_IDENTIFIERS(SEARCH_SENSOR,VERBOSE=VERBOSE)
 
;---------------------
; SEARCH FOR DATA PRODUCTS

    FILE_RESULT = ['']
    FOR IFILTER = 0,N_ELEMENTS(SEARCH_FILTER)-1 DO BEGIN
      RES = FILE_SEARCH(SEARCH_FOLDER,SEARCH_FILTER[IFILTER])
      IF STRCMP(RES[0],'') EQ 0 THEN FILE_RESULT = [FILE_RESULT,RES]
    ENDFOR

   IF N_ELEMENTS(FILE_RESULT) EQ 1 THEN BEGIN
      PRINT, 'DIM_INTER INGEST: ERROR, NO INPUT PRODUCTS FOUND'
      RETURN,0
   ENDIF
  
  FILE_RESULT = FILE_RESULT[1:N_ELEMENTS(FILE_RESULT)-1]
  
  IF STRCMP(FILE_RESULT[0],'') EQ 1 THEN BEGIN
    PRINT, 'DIM_INTER INGEST: ERROR, NO INPUT PRODUCTS FOUND'
    RETURN,0
  ENDIF

;----------------------
; CREATE A NEW ARRAY TO CONTAIN ALL RELEVANT DATA; PATHNAME,FILENAME,REGION,SENSOR,PROC_VER,NEW_PRD

  NB_FILES    = N_ELEMENTS(FILE_RESULT)
  FILE_ARRAY  = MAKE_ARRAY(6,NB_FILES,/STRING)
  FILE_ARRAY[0,*] = FILE_RESULT

;----------------------
;  READ THE DIMITRI DATABASE 

  TEMP = STRSPLIT(FILE_RESULT[0],DELIM,/EXTRACT)
  TEMP_INF = WHERE(STRCMP(TEMP,'Input') EQ 1)
  TEMP_INF = TEMP_INF(N_ELEMENTS(TEMP_INF)-1)
  DIMITRI_DB = GET_DIMITRI_LOCATION('DATABASE',VERBOSE=VERBOSE)
  SITE_FILE = GET_DIMITRI_LOCATION('SITE_DATA',VERBOSE=VERBOSE)
  
  DB_TEMP = FILE_INFO(DIMITRI_DB)
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'DIM_INTER INGEST: CHECKING EXISTENCE OF DATABASE FILE'  
  IF DB_TEMP.EXISTS EQ 0 THEN BEGIN
    DB_CHECK = 0
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'DIM_INTER INGEST: DATABASE FILE NOT FOUND'
  ENDIF ELSE BEGIN
    DB_CHECK = 1
    DB_TEMPLATE = GET_DIMITRI_TEMPLATE(1,/TEMPLATE)
    DB_DATA = READ_ASCII(DIMITRI_DB,TEMPLATE=DB_TEMPLATE)
  ENDELSE

;----------------------
; POPULATE ARRAY WITH INFORMATION

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'DIM_INTER INGEST: CHECKING WHICH FILES ARE ALREADY INGESTED'
  FOR IN_FILE = 0l,NB_FILES-1 DO BEGIN
    TEMP = STRSPLIT(FILE_RESULT[IN_FILE],DELIM,/EXTRACT)
    TEMP_INF = WHERE(STRCMP(TEMP,'Input') EQ 1)
    TEMP_INF = TEMP_INF(N_ELEMENTS(TEMP_INF)-1)
    FILE_ARRAY[2,IN_FILE] = STRMID(TEMP[TEMP_INF+1],5,STRLEN(TEMP[TEMP_INF+1])) 
    FILE_ARRAY[3,IN_FILE] = TEMP[TEMP_INF+2]
    FILE_ARRAY[4,IN_FILE] = STRMID(TEMP[TEMP_INF+3],5,STRLEN(TEMP[TEMP_INF+3])) 
    FILE_ARRAY[1,IN_FILE] = TEMP[N_ELEMENTS(TEMP)-1];FILENAME
  
    IF STRCMP(FILE_ARRAY[3,IN_FILE],'VEGETATION') EQ 1 THEN BEGIN
      FILE_ARRAY[1,IN_FILE] = STRJOIN(TEMP[N_ELEMENTS(TEMP)-3:N_ELEMENTS(TEMP)-1],'_')
    ENDIF

    IF DB_CHECK EQ 1 THEN BEGIN
      RES = WHERE(STRCMP(DB_DATA.FILENAME,FILE_ARRAY[1,IN_FILE]) EQ 1 and STRCMP(DB_DATA.region,ingest_region) EQ 1)
      FILE_ARRAY[5,IN_FILE]='0' 
      IF RES[0] EQ -1 THEN FILE_ARRAY[5,IN_FILE]='1' 
    ENDIF ELSE FILE_ARRAY[5,IN_FILE]='1'

;---------------------
; ADD MODISA HDF EXCEPTION

    IF STRCMP(FILE_ARRAY[3,IN_FILE],'MODISA') EQ 1 THEN BEGIN
      TEMP = FILEINFO(FILE_ARRAY[0,IN_FILE])
      IF TEMP.HDF EQ 0 THEN FILE_ARRAY[5,IN_FILE]='0'
    ENDIF

  ENDFOR

;---------------------
; FIND THE UNIQUE VALUES

  UNIQ_SITE = FILE_ARRAY[2,UNIQ(FILE_ARRAY[2,*], SORT(FILE_ARRAY[2,*]))]
  UNIQ_SENS = FILE_ARRAY[3,UNIQ(FILE_ARRAY[3,*], SORT(FILE_ARRAY[3,*]))]
  UNIQ_PROC = FILE_ARRAY[4,UNIQ(FILE_ARRAY[4,*], SORT(FILE_ARRAY[4,*]))]

  IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT, 'DIM_INTER INGEST: UNIQUE SITES: ',UNIQ_SITE
    PRINT, 'DIM_INTER INGEST: UNIQUE SENSORS: ',UNIQ_SENS
    PRINT, 'DIM_INTER INGEST: UNIQUE PROCESSING VERSIONS: ',UNIQ_PROC
  ENDIF

  TEMP = FIX(FILE_ARRAY[5,*])
  IF TOTAL(TEMP) EQ 0 THEN BEGIN
    PRINT, 'DIM_INTER INGEST: NO NEW PRODUCTS FOR INGESTION'
    RETURN,0
  ENDIF

;---------------------
; LOOP OVER ALL COMBINATIONS AND INGEST DATA

  FOR USITE = 0,N_ELEMENTS(UNIQ_SITE)-1 DO BEGIN
  ICOORDS = GET_SITE_COORDINATES(UNIQ_SITE[USITE],SITE_FILE,VERBOSE=VERBOSE)     
    
    FOR USENS = 0,N_ELEMENTS(UNIQ_SENS)-1 DO BEGIN
      FOR UPROC = 0,N_ELEMENTS(UNIQ_PROC)-1 DO BEGIN

;---------------------
; FIND ALL MATCHING NEW PRODUCTS  

        IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIM_INTER INGEST: SITE - ',UNIQ_SITE[USITE],', SENSOR - ',UNIQ_SENS[USENS],', PROC_VER - ',UNIQ_PROC[UPROC]
       
        RES = WHERE(STRCMP(FILE_ARRAY[2,*],UNIQ_SITE[USITE]) EQ 1 AND $
                    STRCMP(FILE_ARRAY[3,*],UNIQ_SENS[USENS]) EQ 1 AND $
                    STRCMP(FILE_ARRAY[4,*],UNIQ_PROC[UPROC]) EQ 1 AND $
                    STRCMP(FILE_ARRAY[5,*],'1') EQ 1)
                      
        IF RES[0] GT -1 THEN IFILES = FILE_ARRAY[0,RES] ELSE GOTO,NO_IFILES

        CASE UNIQ_SENS[USENS] OF
        'AATSR'       : RES = INGEST_AATSR_PRODUCT(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  ENDIAN_SZE=ENDIAN_SIZE,COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)

        'ATSR2'       : RES = INGEST_ATSR2_PRODUCT(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  ENDIAN_SZE=ENDIAN_SIZE,COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)
 
        'MERIS'       : RES = INGEST_MERIS_PRODUCT(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  ENDIAN_SZE=ENDIAN_SIZE,COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)
 
        'MODISA'      : BEGIN
                        CASE STRUPCASE(!VERSION.OS_FAMILY) OF
                        'WINDOWS':  RES = INGEST_MODISA_PRODUCT(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)
                        'UNIX':     RES = INGEST_MODISA_PRODUCT_LINUX(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)
                        ENDCASE
                        END

        'PARASOL'     : RES = INGEST_PARASOL_PRODUCT(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  ENDIAN_SZE=ENDIAN_SIZE,COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)

        'VEGETATION'  : RES = INGEST_VEGETATION_PRODUCT(IFILES,INPUT_FOLDER=INPUT_FOLDER,ICOORDS=ICOORDS,$
                                                  COLOUR_TABLE=COLOUR_TABLE,$
                                                  PLOT_XSIZE=PLOT_XSIZE,PLOT_YSIZE=PLOT_YSIZE,VERBOSE=VERBOSE)
        ENDCASE
        
        IF RES LT 0 THEN BEGIN
          PRINT, 'DIM_INTER INGEST: ERROR ENCOUNTERED DURING PRODUCT INGESTION' 
          RETURN,-1
        ENDIF
    
        NO_IFILES:
      ENDFOR
    ENDFOR
  ENDFOR

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'DIM_INTER INGEST: DIMITRI INGESTION COMPLETE'
  RETURN,1
  
END
