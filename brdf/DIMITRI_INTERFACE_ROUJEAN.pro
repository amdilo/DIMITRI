;**************************************************************************************
;**************************************************************************************
;+
; NAME:
;      DIMITRI_INTERFACE_ROUJEAN       
;
; PURPOSE:
;*      INTERFACES BETWEEN THE DIMITRI HMI AND THE ROUJEAN_BRDF FUNCTION
;* 
; CALLING SEQUENCE:
;*      RES = DIMITRI_INTERFACE_ROUJEAN(OUTPUT_FOLDER,IB_REGIONS,IB_REF_SENSORS,IB_REF_VERS)
;* 
; INPUTS:
;*      OUTPUT_FOLDER     - THE FULL PATH OF THE OUTPUT FOLDER  
;*      IB_REGION         - A STRING ARRAY OF VALIDAITON SITE NAMES (E.G. ['Uyuni'])]
;*      REF_SENSORS       - A STRING ARRAY CONTAINING THE SENSOR NAMES TO BE TREATED AS 
;*                          REFERENCE SENSORS DURING BRDF COMPUTATION
;*      REF_PROC_VERS     - A STRING ARRAY OF THE REFERENCE SENSOR PROCESSING VERSIONS
;*
; KEYWORDS:
;*      BRDF_BIN          - AN INTEGER OF THE BINNING PERIOD REQUIRED (E.G. 5 = 5 DAY BINS)
;*      NO_PLOTS          - SET TO STOP THE ROUTINE GENERATING BRDF PLOTS FOR EACH BAND AND EACH BIN
;*      ALL               - SET TO RUN BRDF COMPUTATION ON ALL COMBINATIONS OF REF-CAL 
;*                          SENSOR PROVIDED. IF NOT SET THEN REF_SENSORS AND CAL_SENSORS 
;*                          MUST BE THE SAME DIMENSIONS
;*      VERBOSE           - PROCESSING STATUS OUTPUTS
;*
; OUTPUTS:
;*      STATUS            - 1: NO ERRORS REPORTED, (-1) OR 0: ERRORS DURING INGESTION 
;*
; COMMON BLOCKS:
;*      NONE
;*
; MODIFICATION HISTORY:
;*                    - M BOUVET  - PROTOTYPE DIMITRI VERSION
;*        25 JAN 2011 - C KENT    - DIMITRI-2 V1.0
;*        22 MAR 2011 - C KENT    - ADDED CONFIGURAITON FILE DEPENDENCE
;*        15 APR 2011 - C KENT    - MINOR BUG FIXES
;*
; VALIDATION HISTORY:
;*        15 APR 2011 - C KENT    - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                                  COMPILATION AND OPERATION. TESTED ON MERIS 2ND REPROCESSING 
;*                                  WITH MERIS 3RD REPROCESSING AND MODISA COLLECTION 5 
;-
;**************************************************************************************
;**************************************************************************************

FUNCTION DIMITRI_INTERFACE_ROUJEAN,OUTPUT_FOLDER,IB_REGIONS,IB_REF_SENSORS,IB_REF_VERS,$
                                   BRDF_BIN=BRDF_BIN,BRDF_LIM=BRDF_LIM,NO_PLOTS=NO_PLOTS,ALL=ALL,VERBOSE=VERBOSE

;-----------------------------------------
; CHECK OUTPUT_FOLDER EXISTS 
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_ROUJEAN: STARTING INTERFACE MODULE'
  RES = FILE_INFO(OUTPUT_FOLDER)
  IF RES.EXISTS EQ 0 THEN BEGIN
    PRINT,"DIMITRI_INTERFACE_ROUJEAN: OUTPUT FOLDER DOESN'T EXIST"
    RETURN,-1
  ENDIF

;----------------------------------------
; CHECK BRDF BIN SUPPLIED
  
  CFIG_DATA = GET_DIMITRI_CONFIGURATION() 
  
  IF N_ELEMENTS(BRDF_BIN) EQ 0 THEN BEGIN
    PRINT,"DIMITRI_INTERFACE_ROUJEAN: NO BRDF BIN PERIOD SUPPLIED, USING DEFAULT OF 5 DAYS"
    BRDF_BIN = CFIG_DATA.(1)[10]
  ENDIF
  
  N_REFS = N_ELEMENTS(IB_REF_SENSORS)
  N_REFP = N_ELEMENTS(IB_REF_VERS)
  N_REGS = N_ELEMENTS(IB_REGIONS)
 
  IF NOT KEYWORD_SET(ALL) AND $
    (N_REFS+N_REFP)/2 NE N_REFS THEN BEGIN
    PRINT,'DIMITRI_INTERFACE_ROUJEAN: ERROR, INPUT SENSOR ARRAYS NOT CONSISTENT
    RETURN,-1 
  ENDIF
  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_ROUJEAN: INPUT SENSOR ARRAYS NOMINAL'

;-----------------------------------------
; CREATE NEW ARRAYS TO CONTAIN SENSOR INFORMATION
  
  IF NOT KEYWORD_SET(ALL) THEN BEGIN
    RREFS = IB_REF_SENSORS & RREFP = IB_REF_VERS & RREGS = IB_REGIONS
  ENDIF ELSE BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_ROUJEAN: ALL COMBINATIONS KEYWORD SET, CREATING NEW ARRAYS'
    N_COM = N_REFS*N_REFP*N_REGS
    RREFS = STRARR(N_COM) & RREFP = STRARR(N_COM) & RREGS = STRARR(N_COM)
    REF_COUNT=0
    FOR NR=0,N_REGS-1 DO BEGIN
      FOR NS=0,N_REFS-1 DO BEGIN
        FOR NP=0,N_REFP-1 DO BEGIN
          RREGS[REF_COUNT] = IB_REGIONS[NR]
          RREFS[REF_COUNT] = IB_REF_SENSORS[NS]
          RREFP[REF_COUNT] = IB_REF_VERS[NP]
          REF_COUNT++
        ENDFOR
      ENDFOR
    ENDFOR
  ENDELSE

;-----------------------------------------
; LOOP OVER THE REQUIRED COMBINATIONS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_ROUJEAN: STARTING LOOP ON REQUIRED SENSOR COMBINATIONS'
  FOR DI_BRDF = 0,N_ELEMENTS(RREFS)-1 DO BEGIN
      RES = ROUJEAN_BRDF(OUTPUT_FOLDER,RREGS[DI_BRDF],RREFS[DI_BRDF],RREFP[DI_BRDF],$
                         BRDF_BIN,NO_PLOTS=NO_PLOTS,NB_ACQUI_LIMIT=BRDF_LIM,VERBOSE=VERBOSE)
     
      IF RES EQ -1 THEN BEGIN
        PRINT,'DIMITRI_INTERFACE_ROUJEAN: FATAL ERROR ENCOUNTERED'
        RETURN,-1
      ENDIF
  ENDFOR

;----------------------------------------
; RETURN NO FATAL ERRORS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_ROUJEAN: COMPLETED RECALIBRATION, NO FATAL ERRORS IDENTIFIED'  
  RETURN,1

END