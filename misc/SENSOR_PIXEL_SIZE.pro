;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      SENSOR_PIXEL_SIZE       
;* 
;* PURPOSE:
;*      RETURNS THE PIXEL RESOLUTION OR AREA FOR THE DIMITRI SENSORS
;* 
;* CALLING SEQUENCE:
;*      RES = SENSOR_PIXEL_SIZE(SENSOR_PX)      
;* 
;* INPUTS:
;*      SENSOR_PX - A STRING CONTAINING THE NAME OF THE SENSOR OF INTEREST
;*
;* KEYWORDS:
;*      AREA      - SET TO RETURN THE AREA OF EACH PIXEL IN KM^2
;*      VERBOSE   - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      PX_RES    - A FLOAT OF THE RESOLTION/AREA (KM/KM^2)
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      23 DEC 2010 - C KENT   - DIMITRI-2 V1.0
;*      21 MAR 2011 - C KENT   - MODIFIED FILE DEFINITION TO USE GET_DIMITRI_LOCATION
;*
;* VALIDATION HISTORY:
;*      23 DEC 2010 - C KENT   - WINDOWS 32-BIT MACHINE IDL 7.1: COMPILATION AND CALLING SUCCESSFUL
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION SENSOR_PIXEL_SIZE,SENSOR_PX,AREA=AREA,VERBOSE=VERBOSE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'SENSOR_PIXEL_SIZE: RETRIEVEING DATA FOR SENSOR - ',SENSOR_PX
 
;------------------------------------
; DEFINE SENSOR FILE
  
  SPS_FILE = GET_DIMITRI_LOCATION('SENSOR_DATA')
  RES = FILE_INFO(SPS_FILE)
  IF RES.EXISTS EQ 0 THEN BEGIN
    PRINT, 'SENSOR_PIXEL_SIZE: ERROR, SENSOR INFORMATION FILE NOT FOUND'
    RETURN,-1
  ENDIF
  
;------------------------------------
; GET SENSOR DATA TEMPLATE
  
  IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT,'SENSOR_PIXEL_SIZE: RETRIEVEING SENSOR DATA TEMPLATE'
    TEMP = GET_DIMITRI_SENSOR_DATA_TEMPLATE(/VERBOSE)
  ENDIF ELSE TEMP = GET_DIMITRI_SENSOR_DATA_TEMPLATE()

;------------------------------------
; FIND SENSOR MATCH WHITHIN FILE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'SENSOR_PIXEL_SIZE: SEARCHING SENSOR FILE FOR INPUT SENSOR' 
  SPS_DATA = READ_ASCII(SPS_FILE,TEMPLATE=TEMP)
  RES = WHERE(STRMATCH(SPS_DATA.SENSOR_ID,SENSOR_PX) EQ 1)

;------------------------------------
; IF NO MATCH THEN ERROR AND RETURN
  
  IF RES[0] LT 0 THEN BEGIN
    PRINT,'SENSOR_PIXEL_SIZE: ERROR, NO SENSOR MATCH'
    RETURN,-1
  ENDIF
  PX_RES = SPS_DATA.PIX_RES[RES]

;------------------------------------
; IF AREA REQUIRED THEN SQUARE THE RESOLUTION
  
  IF KEYWORD_SET(AREA) THEN PX_RES = PX_RES^2
  RETURN,PX_RES

END