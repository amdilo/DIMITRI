;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*     SPECTRAL_DEP
;* 
;* PURPOSE:
;*      TRANSFER AEROSOL OPTICAL THICKNESS FROM A REFERENCE BAND TO ANOTHER GIVEN BAND
;* 
;* CALLING SEQUENCE:
;*      RES = COMPUTE_TRR(WIND, VZA, SZA, IBAND)
;* 
;* INPUTS:
;*      TAUA_REF - THE OPTICAL THICKNESS AT REFERENCE BAND
;*      IREF     - THE INDEX OF REFERENCE WAVELENGTH FOR GIVEN SENSOR
;*      IBAND    - THE INDEX OF WAVELENGTH FOR GIVEN SENSOR
;*
;* KEYWORDS:
;*      VERBOSE          - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      TAUA             - AEROSOL OPTICAL THICKNESS AT BAND IBAND
;*
;* COMMON BLOCKS:
;*      RTM_LUT
;*
;* MODIFICATION HISTORY:
;*        01 NOV 2013 - C MAZERAN - FIRST IMPLEMENTATION
;*
;* VALIDATION HISTORY:
;*        01 NOV 2013 - C MAZERAN - LINUX 64-BIT MACHINE IDL 8.2, NOMINAL COMPILATION AND OPERATION. 
;*
;**************************************************************************************
FUNCTION SPECTRAL_DEP, TAUA_REF, IREF, IBAND

COMMON RTM_LUT

;-----------------------------------------
; DEFINE CURRENT FUNCTION NAME

 FCT_NAME = "SPECTRAL_DEP"

 ;-----------------------------------------
 ; CHECK INPUTS CORRESPOND TO
 ; A ONE-DIMENSIONNAL ARRAY

 S = SIZE(TAUA_REF,/N_DIMENSIONS)

 IF S NE 1 THEN BEGIN
   PRINT, FCT_NAME+":ERROR, WORKS ONLY FOR 1D ARRAY"
   RETURN, -1
 ENDIF

;-----------------------------------------
; COMPUTE INDICES AND WEIGHTS FOR THE REF BAND

 N=N_ELEMENTS(TAUA_REF)
 INDEX  = INTARR(N)
 WEIGHT = DBLARR(N)

 XREF = REFORM(TAUA_LUT.DATA[*,IREF])
 
 INDEX[*]  = VALUE_LOCATE(XREF, TAUA_REF)

 ID=WHERE(INDEX GE 0 AND INDEX LT N_ELEMENTS(XREF)-1, COUNT, NCOMPLEMENT=NCOUNT)
 IF COUNT NE 0 THEN WEIGHT[ID]=(TAUA_REF[ID]-XREF[INDEX[ID]])/(XREF[INDEX[ID]+1]-XREF[INDEX[ID]])
 IF NCOUNT NE 0 THEN BEGIN
   ID=WHERE(INDEX[*] EQ -1 )
   IF ID[0] NE -1 THEN BEGIN
     INDEX[ID]   = 0
     WEIGHT[ID]  = 0.
   ENDIF
   ID=WHERE(INDEX[*] EQ N_ELEMENTS(XREF)-1 )
   IF ID[0] NE -1 THEN BEGIN
     INDEX[ID]   = N_ELEMENTS(XREF)-2
     WEIGHT[ID]  = 1.
   ENDIF
 ENDIF

 
;-----------------------------------------
; INTERPOLATE AT GIVEN BAND

 ;TAUA=(1-WEIGHT)*TAUA_LUT.DATA[INDEX,IBAND]+WEIGHT*TAUA_LUT.DATA[INDEX+1,IBAND]
  TAUA = DBLARR(N)
  I=WHERE(INDEX NE 0, COUNT, COMPLEMENT=IZ, NCOMPLEMENT=NCOUNT)
  IF COUNT NE 0 THEN TAUA[I] = TAUA_REF[I]*((1.-WEIGHT[I])*TAUA_LUT.DATA[INDEX[I],IBAND]/XREF[INDEX[I]]+WEIGHT[I]*TAUA_LUT.DATA[INDEX[I]+1,IBAND]/XREF[INDEX[I]+1])
  IF NCOUNT NE 0 THEN TAUA[IZ] = TAUA_REF[IZ]*((1.-WEIGHT[IZ])+WEIGHT[IZ]*TAUA_LUT.DATA[INDEX[IZ]+1,IBAND]/XREF[INDEX[IZ]+1]) 
  ;IF NCOUNT NE 0 THEN TAUA[IZ] = TAUA_REF[IZ]*(WEIGHT[IZ]*TAUA_LUT.DATA[INDEX[IZ]+1,IBAND]/XREF[INDEX[IZ]+1]) 

 RETURN, TAUA

END
