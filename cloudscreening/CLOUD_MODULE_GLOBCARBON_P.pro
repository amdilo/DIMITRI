;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      CLOUD_MODULE_GLOBCARBON_P       
;* 
;* PURPOSE:
;*      THIS FUNCTION PERFORMS THE CLOUDSCREENING ALGORITHM DEVELOPED FOR THE GLOBCARBON 
;*      PROJECT. THE ALGORITHM PERFORMS 2 CLOUD SCREENING PASSES ON PIXELS FLAGGED AS 
;*      "BRIGHT" BY THE OPERATIONAL MERIS BRIGHT PIXEL FLAG ALGORITHM. THIS ALGORITHM 
;*      HAS BEEN MODIFIED TO WORK USING FIXED THRESHOLDS AND WAVEBAND AVAILABLE TO PARASOL
;* 
;* CALLING SEQUENCE:
;*      RES = CLOUD_MODULE_GLOBCARBON_P(SITE_TYPE,GCARBON_REF,TOA_GEO)    
;* 
;* INPUTS:
;*      SITE_TYPE  - A STRING OF THE DIMITRI VALIDATION SITE TYPE (E.G. 'OCEANIC')'
;*      GCARBON_REF- A FLOAT ARRAY CONTAINING THE TOA REFLECTANCE AT 443,705,750 AND 
;*                   865NM [NB_PIXELS,RHO_BANDS]
;*      TOA_GEO    - A 3 OR 4 ELEMENT ARRAY CONTAINING THE SZA AND VZA VALUES AND EITHER
;*                   RAA OR SAA AND VAA 
;*
;* KEYWORDS:
;*      VERBOSE    - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      CLASS_PIXELS - AN INTEGER ARRAY OF SIXE NUMBER OF PIXELS, 0 MEANS CLEAR PIXEL, 
;*                     1 MEANS CLOUDY
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      14 MAY 2011 - C KENT   - DIMITRI-2 V1.0
;*      14 MAY 2011 - C KENT   - ADDED BAND INDEX VALUES
;*
;* VALIDATION HISTORY:
;*      14 MAY 2011 - C KENT   - NOMINAL COMPILATION AND OPERATION ON WINDOWS 32BIT 
;*                               IDL 7.1 AND LINUX 64BIT IDL 8.0 
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION CLOUD_MODULE_GLOBCARBON_P,SITE_TYPE,GCARBON_REF,TOA_GEO,VERBOSE=VERBOSE

;-------------------------
; RESTORE BRIGHT LUT INFORMATION

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON_P: RESTORING BRIGHT LUT INFORMATION'
  TEMP_LUT = GET_DIMITRI_LOCATION('BRIGHT_LUT')
  RESTORE,TEMP_LUT

;-------------------------
; DEFINE SNOW AND CLOUD THRESHOLDS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON_P: DEFINING THRESHOLDS AND PARAMETERS'
  SNOW_THRESH   = 0.9386
  CLOUD_THRESH  = 1.736

;-------------------------
; DEFINE PARAMETERS FOR CLOUD SCREENING

  TEMP_DIMS     = SIZE(GCARBON_REF,/DIMENSIONS)
  NB_PIXELS     = TEMP_DIMS[0]
  BRIGHT_PIXELS = INTARR(NB_PIXELS)
  CLASS_PIXELS  = INTARR(NB_PIXELS)

  ICLEAR  = 0
  ICLOUD  = 1
  ISNOW   = 2
  IBRIGHT = 3
  
  B1 = 0
  B2 = 1
  B3 = 2
  B4 = 3

;-------------------------
; FOR EACH PIXEL FIND OUT IF IT'S "BRIGHT"

  FOR GCI=0L,NB_PIXELS-1 DO BRIGHT_PIXELS[GCI] = DIMITRI_CLOUD_SCREENING_BRIGHT_RHO(GCARBON_REF[GCI,B1],TOA_GEO[GCI,*],BRIGHT_RHO)

;-------------------------
; IF NO BRIGHT PIXELS THEN RETURN ALL PIXELS AS CLEAR

  BRIGHTF = WHERE(BRIGHT_PIXELS EQ 1)
  IF BRIGHTF[0] EQ -1 THEN RETURN,CLASS_PIXELS
  
;-------------------------
; IF OCEANIC SITE THEN BRIGHT PIXELS ARE CLOUD

  IF SITE_TYPE EQ 'OCEANIC' THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON_P: OCEANIC EXCEPTION, BRIGHT PIXELS ARE CLOUD'
    CLASS_PIXELS[BRIGHTF] = ICLOUD
    RETURN,CLASS_PIXELS
  ENDIF ELSE CLASS_PIXELS[BRIGHTF] =  IBRIGHT

;-------------------------
; PASS 1: PERFORM RATIO OF B9/B13 
; TO DEFINE EITHER SNOW OR CLOUD PIXELS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON_P: PERFORMING PASS 1'
  PASS_1 = GCARBON_REF[*,B2]/GCARBON_REF[*,B4]
  SNOWF = WHERE(PASS_1 GT SNOW_THRESH AND CLASS_PIXELS EQ IBRIGHT)
  IF SNOWF[0] GT -1 THEN CLASS_PIXELS[SNOWF] = ISNOW 

;-------------------------
; PASS 2: OF PIXELS FLAGGED AS 
; CLOUD USE B2/B10 TO DETERMINE CLOUD OR BRIGHT LAND

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON_P: PERFORMING PASS 2'
  PASS_2 = GCARBON_REF[*,B1]/GCARBON_REF[*,B3]
  CLOUDF = WHERE(PASS_2 GT CLOUD_THRESH AND CLASS_PIXELS NE ISNOW AND CLASS_PIXELS EQ IBRIGHT)
  IF CLOUDF[0] GT -1 THEN CLASS_PIXELS[CLOUDF] = ICLOUD 

;-------------------------
; COMPUTE STATS FOR PRODUCT

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON_P: COMPUTING STATS'
  STATS_SNOW  = 100*FLOAT(N_ELEMENTS(SNOWF))/FLOAT(NB_PIXELS)
  STATS_CLOUD = 100*FLOAT(N_ELEMENTS(cloudF))/FLOAT(NB_PIXELS)
  STATS_CLEAR = 100.0-STATS_SNOW-STATS_CLOUD
 
  IF KEYWORD_SET(VERBOSE) THEN PRINT,$
    'CLOUD_MODULE_GLOBCARBON_P: SNOW: ',STATS_SNOW,' CLOUD: ',STATS_CLOUD,' CLEAR: ',STATS_CLEAR

;-------------------------
; RESET ALL SNOW PIXELS AS CLEAR

  IF SNOWF[0] GT -1 THEN CLASS_PIXELS[SNOWF] = ICLEAR

;-------------------------
; RESET ALL BRIGHT PIXELS AS CLEAR
  
  RES = WHERE(CLASS_PIXELS EQ IBRIGHT)
  IF RES[0] GT -1 THEN CLASS_PIXELS[RES] = ICLEAR  
    
  RETURN,CLASS_PIXELS

END