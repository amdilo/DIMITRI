;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      DHMI_DESERT_ARG    
;* 
;* PURPOSE:
;*      THIS PROGRAM DISPLAYS A WIDGET ALLOWING SPECIFICATION OF THE REQUIRED PARAMETERS 
;*      TO PERFORM THE TOA SIMULATION USING Hyperspectral-BRDF METHODOLOGY 
;*
;*
;* CALLING SEQUENCE:
;*      DHMI_DESERT_ARG
;*
;* INPUTS:
;*      NONE
;*
;* KEYWORDS:
;*      GROUP_LEADER - THE ID OF ANOTHER WIDGET TO BE USED AS THE GROUP LEADER
;*      VERBOSE      - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      NONE
;*
;* COMMON BLOCKS:
;*      DHMI_DATABASE - CONTAINS THE DATABASE DATA FOR THE DIMITRI HMI
;*
;* MODIFICATION HISTORY:
;*      15 JAN 2015 - B ALHAMMOUD - DIMITRI-3 V1.0
;*      21 JAN 2015 - B ALHAMMOUD    - UPDATED TO DIMITRI-3.1.1
;*      16 FEB 2015 - B ALHAMMOUD    - ADDED DIMITRI_VERSION AS VARIABLE
;*      17 MAR 2015 - NCG / MAGELLIUM - UPDATED WITH DIMITRI V4 SPECIFICATIONS
;*
;* VALIDATION HISTORY:
;*      15 JAN 2015 - B ALHAMMOUD - LINUX 64-BIT IDL 8.2 NOMINAL COMPILATION AND OPERATION       
;*      30 MAR 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL (DIMITRI V4.0) 
;*
;**************************************************************************************
;**************************************************************************************

PRO DHMI_DESERT_ARG_START,EVENT

 
COMMON DHMI_DATABASE 
 
 ;--------------------------
 ; RETRIEVE WIDGET INFORMATION
 
   WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_P5_INFO, /NO_COPY

;---------------------------
; RETRIEVE ALL PARAMETERS

  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG->START: RETRIEVING PARAMETERS FROM WIDGET FIELDS' 
  DHMI_P5_INFO.FSCP5_REGION->GETPROPERTY, VALUE=P5_REGION
  DHMI_P5_INFO.FSCP5_SENSOR->GETPROPERTY,  VALUE=P5_SENSOR
  DHMI_P5_INFO.FSCP5_PROC->GETPROPERTY,  VALUE=P5_PROC
  DHMI_P5_INFO.FSCP5_YEAR->GETPROPERTY,  VALUE=P5_YEAR
  DHMI_P5_INFO.FSCP5_CPU_NB->GETPROPERTY,    VALUE=P5_CPU_NB
  DHMI_P5_INFO.FSCP5_PHOTON_NB->GETPROPERTY,    VALUE=P5_PHOTON_NB

;---------------------------
; CHECK USER VALUES

  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG->START: CHECKING USER PARAMETERS'
  ERR_CPU = 0
  ERR_PHOTON  = 0
  CPUMAX=!CPU.HW_NCPU     ; Retrive the number of core on the platform (for linux)

  
  IF (P5_PHOTON_NB LT 1) THEN ERR_PHOTON=1
  IF (P5_CPU_NB LT 1) THEN ERR_CPU=1
  IF (P5_CPU_NB GT CPUMAX) THEN ERR_CPU=1
  IF ERR_PHOTON OR ERR_CPU THEN BEGIN
    MSG = ['Input Error']
    IF ERR_PHOTON THEN MSG = [MSG, 'Photon Number must be > 0 ']
    IF ERR_CPU THEN MSG = [MSG, 'CPU Number must be within [1, '+strtrim(CPUMAX,1)+']']
    TEMP = DIALOG_MESSAGE(MSG,/INFORMATION,/CENTER)
    GOTO,P5_ERR
  ENDIF

;--------------------------
; GET SCREEN DIMENSIONS FOR 
; CENTERING INFO WIDGET

  DIMS  = GET_SCREEN_SIZE()
  XSIZE = 200
  YSIZE = 60
  XLOC  = (DIMS[0]/2)-(XSIZE/2)
  YLOC  = (DIMS[1]/2)-(YSIZE/2)

  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG->START: CREATING AN INFO WIDGET'
  INFO_WD = WIDGET_BASE(COLUMN=1, XSIZE=XSIZE, YSIZE=YSIZE, TITLE='Please Wait...',XOFFSET=XLOC,YOFFSET=YLOC)
  LBLTXT  = WIDGET_LABEL(INFO_WD,VALUE=' ')
  LBLTXT  = WIDGET_LABEL(INFO_WD,VALUE='Please wait,')
  LBLTXT  = WIDGET_LABEL(INFO_WD,VALUE='Processing...')
  WIDGET_CONTROL, INFO_WD, /REALIZE
  WIDGET_CONTROL, /HOURGLASS

;--------------------------
; DESERT SIMULATION (HsBRDF Methodology)

  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG->START: RUNNING DESERT SIMULATION'  
  
  RES = DIMITRI_INTERFACE_DESERT_ARG(P5_REGION,P5_SENSOR,P5_PROC,P5_YEAR,P5_PHOTON_NB,P5_CPU_NB, VERBOSE=DHMI_P5_INFO.IVERBOSE)
  
;--------------------------
; CLOSE DISPLAY WIDGET AND REPORT STATUS

  P5_ERR:
  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT, 'DHMI_DESERT_ARG->START: DESTROYING THE POP DISPLAY'
  IF N_ELEMENTS(INFO_WD) GT 0 THEN WIDGET_CONTROL,INFO_WD,/DESTROY
  WAIT,0.5

  IF N_ELEMENTS(RES) NE 0 THEN BEGIN
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT, 'DHMI_DESERT_ARG->START: USE OUTPUT TO IDENTIFY CLOSING MESSAGE'
    CASE RES OF
      -1: MSG = ['One or more errors encountered','Please check log, database file and output plots']
       0: MSG = ['No Desert products found']
       1: MSG = ['Successful']
    ENDCASE
    TMP = DIALOG_MESSAGE(['Desert ARG Simulation Process : ','',MSG],/INFORMATION,/CENTER)
  ENDIF

;--------------------------
; RETURN TO DESERT WIDGET 

  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT, 'DHMI_DESERT_ARG->START: RETURNING TO THE DESERT WIDGET'  
  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_P5_INFO, /NO_COPY

END 

;**************************************************************************************
;**************************************************************************************

PRO DHMI_DESERT_ARG_EXIT,EVENT

;--------------------------
; RETRIEVE WIDGET INFORMATION

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_P5_INFO, /NO_COPY
  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG->EXIT: DESTROYING OBJECTS'

;--------------------------
; DESTROY OBJECTS

  OBJ_DESTROY,DHMI_P5_INFO.FSCP5_REGION
  OBJ_DESTROY,DHMI_P5_INFO.FSCP5_SENSOR
  OBJ_DESTROY,DHMI_P5_INFO.FSCP5_PROC
  OBJ_DESTROY,DHMI_P5_INFO.FSCP5_YEAR
  OBJ_DESTROY,DHMI_P5_INFO.FSCP5_CPU_NB
  OBJ_DESTROY,DHMI_P5_INFO.FSCP5_PHOTON_NB

;--------------------------
; DESTROY THE WIDGET

  IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG->EXIT: DESTROYING DESERT WIDGET'
  WIDGET_CONTROL,EVENT.TOP,/DESTROY

END 

;**************************************************************************************
;**************************************************************************************


PRO DHMI_DESERT_ARG_SETUP_CHANGE,EVENT

COMMON DHMI_DATABASE
  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_P5_INFO, /NO_COPY
  WIDGET_CONTROL, EVENT.ID,   GET_UVALUE=ACTION

;--------------------------
; GET THE ACTION TYPE

  ACTION_TYPE = STRMID(ACTION,0,1)

;--------------------------
; UPDATE SENSOR VALUE

  IF ACTION_TYPE EQ 'V' THEN BEGIN
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE SITE FIELD AND INDEX'
    CASE ACTION OF
      'VSITE<':DHMI_P5_INFO.ISITE = DHMI_P5_INFO.ISITE-1
      'VSITE>':DHMI_P5_INFO.ISITE = DHMI_P5_INFO.ISITE+1
    ENDCASE
    IF DHMI_P5_INFO.ISITE LT 0 THEN DHMI_P5_INFO.ISITE = DHMI_P5_INFO.NASITE-1
    IF DHMI_P5_INFO.ISITE EQ DHMI_P5_INFO.NASITE THEN DHMI_P5_INFO.ISITE = 0

    DHMI_P5_INFO.FSCP5_REGION->SETPROPERTY, VALUE=DHMI_P5_INFO.ASITE[DHMI_P5_INFO.ISITE]

;--------------------------
; GET AVAILABLE SENSORS WITHIN REGION

    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE SENSOR FIELD AND INDEX'
    CSITE=DHMI_P5_INFO.ASITE[DHMI_P5_INFO.ISITE]

    TEMP = DHMI_DB_DATA.SENSOR[WHERE(STRMATCH(DHMI_DB_DATA.SITE_NAME,CSITE))]
    TEMP = TEMP[UNIQ(TEMP,SORT(TEMP))]
    DHMI_P5_INFO.ASENS[0:N_ELEMENTS(TEMP)-1] = TEMP
    DHMI_P5_INFO.NASENS = N_ELEMENTS(TEMP)
    DHMI_P5_INFO.ISENS  = 0
    DHMI_P5_INFO.FSCP5_SENSOR->SETPROPERTY, VALUE=DHMI_P5_INFO.ASENS[DHMI_P5_INFO.ISENS]

    GOTO,UPDATE_PROC

  ENDIF

;--------------------------
; UPDATE SENSOR VALUE

  IF ACTION_TYPE EQ 'S' THEN BEGIN
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE SENSOR FIELD AND INDEX'
    CASE ACTION OF
      'SENS<':DHMI_P5_INFO.ISENS = DHMI_P5_INFO.ISENS-1
      'SENS>':DHMI_P5_INFO.ISENS = DHMI_P5_INFO.ISENS+1
    ENDCASE
    IF DHMI_P5_INFO.ISENS LT 0 THEN DHMI_P5_INFO.ISENS = DHMI_P5_INFO.NASENS-1
    IF DHMI_P5_INFO.ISENS EQ DHMI_P5_INFO.NASENS THEN DHMI_P5_INFO.ISENS = 0

    DHMI_P5_INFO.FSCP5_SENSOR->SETPROPERTY, VALUE=DHMI_P5_INFO.ASENS[DHMI_P5_INFO.ISENS]

 ENDIF 

;--------------------------
; GET AVAILABLE PROC_VERS 
; FOR SITE AND SENSOR

    UPDATE_PROC:
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE PROCESSING VERSION FIELD AND INDEX'
    CSITE = DHMI_P5_INFO.ASITE[DHMI_P5_INFO.ISITE]
    CSENS = DHMI_P5_INFO.ASENS[DHMI_P5_INFO.ISENS]

    TEMP = DHMI_DB_DATA.PROCESSING_VERSION[WHERE($
                                                 STRMATCH(DHMI_DB_DATA.SITE_NAME,CSITE) AND $
                                                 STRMATCH(DHMI_DB_DATA.SENSOR,CSENS))]
    TEMP = TEMP[UNIQ(TEMP,SORT(TEMP))]
    DHMI_P5_INFO.APROC[0:N_ELEMENTS(TEMP)-1] = TEMP
    DHMI_P5_INFO.NAPROC = N_ELEMENTS(TEMP)
    DHMI_P5_INFO.IPROC  = 0
    DHMI_P5_INFO.FSCP5_PROC->SETPROPERTY, VALUE=DHMI_P5_INFO.APROC[DHMI_P5_INFO.IPROC]

;--------------------------
; UPDATE PROC VALUE

  IF ACTION_TYPE EQ 'P' THEN BEGIN
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE PROCESSING VERSION FIELD AND INDEX'
    CASE ACTION OF
      'PROC<':DHMI_P5_INFO.IPROC = DHMI_P5_INFO.IPROC-1
      'PROC>':DHMI_P5_INFO.IPROC = DHMI_P5_INFO.IPROC+1
    ENDCASE
    IF DHMI_P5_INFO.IPROC LT 0 THEN DHMI_P5_INFO.IPROC = DHMI_P5_INFO.NAPROC-1
    IF DHMI_P5_INFO.IPROC EQ DHMI_P5_INFO.NAPROC THEN DHMI_P5_INFO.IPROC = 0

    DHMI_P5_INFO.FSCP5_PROC->SETPROPERTY, VALUE=DHMI_P5_INFO.APROC[DHMI_P5_INFO.IPROC]

;--------------------------
; GET AVAILABLE YEARS FOR SITE,
; SENSOR AND PROC VERSION

    UPDATE_YEAR:
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE YEAR FIELD AND INDEX'
    CSITE = DHMI_P5_INFO.ASITE[DHMI_P5_INFO.ISITE]
    CSENS = DHMI_P5_INFO.ASENS[DHMI_P5_INFO.ISENS]
    CPROC = DHMI_P5_INFO.APROC[DHMI_P5_INFO.IPROC]

    TEMP = STRTRIM(STRING(DHMI_DB_DATA.YEAR[WHERE($
                                                  STRMATCH(DHMI_DB_DATA.SITE_NAME,CSITE)       AND $
                                                  STRMATCH(DHMI_DB_DATA.SENSOR,CSENS)       AND $
                                                  STRMATCH(DHMI_DB_DATA.PROCESSING_VERSION,CPROC))]),2)
    TEMP = TEMP[UNIQ(TEMP,SORT(TEMP))]
    DHMI_P5_INFO.AYEAR[0:N_ELEMENTS(TEMP)] = [TEMP,'ALL']
    DHMI_P5_INFO.NAYEAR = N_ELEMENTS(TEMP)+1
    DHMI_P5_INFO.IYEAR=0
    DHMI_P5_INFO.FSCP5_YEAR->SETPROPERTY, VALUE=DHMI_P5_INFO.AYEAR[DHMI_P5_INFO.IYEAR]

 ENDIF 

;--------------------------
; UPDATE YEAR VALUE

  IF ACTION_TYPE EQ 'Y' THEN BEGIN
    IF DHMI_P5_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_DESERT_ARG_SETUP->CHANGE: UPDATING THE YEAR FIELD AND INDEX'
    CASE ACTION OF
      'YEAR<':DHMI_P5_INFO.IYEAR = DHMI_P5_INFO.IYEAR-1
      'YEAR>':DHMI_P5_INFO.IYEAR = DHMI_P5_INFO.IYEAR+1
    ENDCASE
    IF DHMI_P5_INFO.IYEAR LT 0 THEN DHMI_P5_INFO.IYEAR = DHMI_P5_INFO.NAYEAR-1
    IF DHMI_P5_INFO.IYEAR EQ DHMI_P5_INFO.NAYEAR THEN DHMI_P5_INFO.IYEAR = 0

    DHMI_P5_INFO.FSCP5_YEAR->SETPROPERTY, VALUE=DHMI_P5_INFO.AYEAR[DHMI_P5_INFO.IYEAR]
 ENDIF 


;--------------------------
; RETRUN TO THE WIDGET

  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_P5_INFO, /NO_COPY

END 

;**************************************************************************************
;**************************************************************************************

PRO DHMI_DESERT_ARG,GROUP_LEADER=GROUP_LEADER,VERBOSE=VERBOSE

COMMON DHMI_DATABASE

;--------------------------
; FIND MAIN DIMITRI FOLDER AND DELIMITER

  IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT,'DHMI_DESERT_ARG: STARTING DESERT HMI ROUTINE'
    IVERBOSE=1
  ENDIF ELSE IVERBOSE=0
  IF STRUPCASE(!VERSION.OS_FAMILY) EQ 'WINDOWS' THEN WIN_FLAG = 1 ELSE WIN_FLAG = 0
 
;--------------------------
; DEFINE BASE PARAMETERS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: DEFINING BASE PARAMETERS'
  CFIG_DATA = GET_DIMITRI_CONFIGURATION() 

  BASE_CPU     = FIX(CFIG_DATA.(1)[23])
  BASE_PHOTON  = FIX(CFIG_DATA.(1)[24])
  
  OPT_BTN   = 60
  SML_BTNX  = 30
  SML_BTNY  = 10 
  SML_DEC   = 2
  SML_FSC_X = 7

;--------------------------
; GET LIST OF ALL OUTPUT FOLDERS, 
; SITES, SENSORS AND PROCESSING VERSIONS

  ASITES = DHMI_DB_DATA.SITE_NAME[UNIQ(DHMI_DB_DATA.SITE_NAME,SORT(DHMI_DB_DATA.SITE_NAME))]
  USENSS = DHMI_DB_DATA.SENSOR[UNIQ(DHMI_DB_DATA.SENSOR,SORT(DHMI_DB_DATA.SENSOR))]
  UPROCV = DHMI_DB_DATA.PROCESSING_VERSION[UNIQ(DHMI_DB_DATA.PROCESSING_VERSION,$
                                       SORT(DHMI_DB_DATA.PROCESSING_VERSION))]
  UYEARS = DHMI_DB_DATA.YEAR[UNIQ(DHMI_DB_DATA.YEAR,SORT(DHMI_DB_DATA.YEAR))]

;--------------------------  
; SELECT FIRST SITE AND GET 
; AVAILABLE SENSORS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: RETRIEVING AVAILABLE SITES AND SENSORS'
  ASENS = MAKE_ARRAY(N_ELEMENTS(USENSS),/STRING,VALUE='')
  APROC = MAKE_ARRAY(N_ELEMENTS(UPROCV),/STRING,VALUE='')
  AYEAR = MAKE_ARRAY(N_ELEMENTS(UYEARS)+1,/STRING,VALUE='')

  NASITE = N_ELEMENTS(ASITES)
  CSITE  = ASITES[0]
  TEMP   = DHMI_DB_DATA.SENSOR[WHERE(DHMI_DB_DATA.SITE_NAME EQ CSITE)]
  TEMP   = TEMP[UNIQ(TEMP,SORT(TEMP))]
  ASENS[0:N_ELEMENTS(TEMP)-1] = TEMP
  NASENS = N_ELEMENTS(TEMP)
  CSENS  = ASENS[0]

;--------------------------  
; GET AVAILABLE PROCESSING VERSIONS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: RETRIEVING AVAILABLE PROCESSING VERSIONS'
  TEMP    = DHMI_DB_DATA.PROCESSING_VERSION[WHERE(DHMI_DB_DATA.SITE_NAME EQ CSITE AND $
                                                  DHMI_DB_DATA.SENSOR EQ CSENS)]
  TEMP    = TEMP[UNIQ(TEMP,SORT(TEMP))]
  APROC[0:N_ELEMENTS(TEMP)-1] = TEMP
  NAPROC  = N_ELEMENTS(TEMP)
  CPROC   = APROC[0]

;--------------------------  
; GET AVAILABLE YEARS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: RETRIEVING AVAILABLE YEARS'
  TEMP    = STRTRIM(STRING(DHMI_DB_DATA.YEAR[WHERE(DHMI_DB_DATA.SITE_NAME EQ CSITE AND $
                                              DHMI_DB_DATA.SENSOR EQ CSENS AND $
                                              DHMI_DB_DATA.PROCESSING_VERSION EQ CPROC)]),2)
  TEMP    = TEMP[UNIQ(TEMP,SORT(TEMP))]
  AYEAR[0:N_ELEMENTS(TEMP)] = [TEMP,'ALL']
  CYEAR   = AYEAR[0]
  NAYEAR  = N_ELEMENTS(TEMP)+1


;--------------------------
; DEFINE THE MAIN WIDGET 

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: RETRIEVING SCREEN DIMENSIONS FOR WIDGET'
  DIMS  = GET_SCREEN_SIZE()
  IF WIN_FLAG THEN XSIZE = 425 ELSE XSIZE = 490
  YSIZE = 800
  XLOC  = (DIMS[0]/2)-(XSIZE/2)
  YLOC  = (DIMS[1]/2)-(YSIZE/2)

  TOOL = GET_DIMITRI_LOCATION('TOOL')
  DHMI_P5_TLB = WIDGET_BASE(COLUMN=1,TITLE=TOOL+': DESERT SIMULATION SETUP',XSIZE=XSIZE,$
                                  XOFFSET=XLOC,YOFFSET=YLOC)
;--------------------------
; DEFINE WIDGET TO HOLD OUTPUTFOLDER,
; REGION, SENSOR AND CONFIGURATION

  DHMI_P5_TLB_1 = WIDGET_BASE(DHMI_P5_TLB,ROW=5, FRAME=1)
  DHMI_P5_TLB_1_LBL = WIDGET_LABEL(DHMI_P5_TLB_1,VALUE='CASE STUDY:')
  DHMI_P5_TLB_1_LBL = WIDGET_LABEL(DHMI_P5_TLB_1,VALUE='')
  DHMI_P5_TLB_1_LBL = WIDGET_LABEL(DHMI_P5_TLB_1,VALUE='')

  IF WIN_FLAG THEN DHMI_P5_TLB_1_RID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CSITE,TITLE  ='REGION    :',OBJECT=FSCP5_REGION) $
              ELSE DHMI_P5_TLB_1_RID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CSITE,TITLE  ='REGION    :',OBJECT=FSCP5_REGION)
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='<',UVALUE='VSITE<',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='>',UVALUE='VSITE>',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')  
  
  IF WIN_FLAG THEN DHMI_P5_TLB_1_SID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CSENS,TITLE  ='SENSOR    :',OBJECT=FSCP5_SENSOR) $
              ELSE DHMI_P5_TLB_1_SID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CSENS,TITLE  ='SENSOR    :',OBJECT=FSCP5_SENSOR)
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='<',UVALUE='SENS<',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='>',UVALUE='SENS>',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')  

  IF WIN_FLAG THEN DHMI_P5_TLB_1_PID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CPROC,TITLE  ='PROCESSING:',OBJECT=FSCP5_PROC) $
              ELSE DHMI_P5_TLB_1_PID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CPROC,TITLE  ='PROCESSING:',OBJECT=FSCP5_PROC)
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='<',UVALUE='PROC<',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='>',UVALUE='PROC>',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')  

  IF WIN_FLAG THEN DHMI_P5_TLB_1_YID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CYEAR,TITLE  ='YEAR      :',OBJECT=FSCP5_YEAR) $
              ELSE DHMI_P5_TLB_1_YID = FSC_FIELD(DHMI_P5_TLB_1,VALUE=CYEAR,TITLE  ='YEAR      :',OBJECT=FSCP5_YEAR)
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='<',UVALUE='YEAR<',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')
  DHMI_BLK      = WIDGET_BUTTON(DHMI_P5_TLB_1,VALUE='>',UVALUE='YEAR>',EVENT_PRO='DHMI_DESERT_ARG_SETUP_CHANGE')  

       
;--------------------------
; DEFINE WIDGET TO HOLD  
; DESERT CAL PARAMETERS

  DHMI_P5_TLB_2       = WIDGET_BASE(DHMI_P5_TLB,COLUMN=1,FRAME=1)
  DHMI_P5_TLB_2_LBL   = WIDGET_LABEL(DHMI_P5_TLB_2,VALUE=' DESERT SIMULATION PARAMETERS:', /ALIGN_LEFT)

  IF WIN_FLAG THEN DHMI_P5_TLB_2_CPUID = FSC_FIELD(DHMI_P5_TLB_2,VALUE=BASE_CPU,TITLE        ='DEFINE THE NUMBER OF PARALLEL JOBS/CPUS  : ',OBJECT=FSCP5_CPU_NB,DECIMAL=SML_DEC,XSIZE=SML_FSC_X) $  
              ELSE DHMI_P5_TLB_2_CPUID = FSC_FIELD(DHMI_P5_TLB_2,VALUE=BASE_CPU,TITLE        ='DEFINE THE NUMBER OF PARALLEL JOBS/CPUS '+STRING([10B])+'(THE HIGHER THE CPU_NB THE FASTER THE RUN)                    : ',OBJECT=FSCP5_CPU_NB,DECIMAL=SML_DEC,XSIZE=SML_FSC_X)
 
  IF WIN_FLAG THEN DHMI_P5_TLB_2_PHOTNID = FSC_FIELD(DHMI_P5_TLB_2,VALUE=BASE_PHOTON,TITLE   ='DEFINE THE NUMBER OF PHOTONS : ',OBJECT=FSCP5_PHOTON_NB,DECIMAL=SML_DEC,XSIZE=SML_FSC_X) $  
              ELSE DHMI_P5_TLB_2_PHOTNID = FSC_FIELD(DHMI_P5_TLB_2,VALUE=BASE_PHOTON,TITLE   ='DEFINE THE NUMBER OF PHOTONS            '+STRING([10B])+'(THE HIGHER THE PHOTON_NB THE BETTER THE STATISTICAL RESULTS) : ',OBJECT=FSCP5_PHOTON_NB,DECIMAL=SML_DEC,XSIZE=SML_FSC_X)

 ;--------------------------
; DEFINE WIDGET TO HOLD START  
; AND EXIT BUTTONS
  
  DHMI_P5_TLB_3       = WIDGET_BASE(DHMI_P5_TLB,ROW=1,/ALIGN_RIGHT)
  DHMI_P5_TLB_3_BTN   = WIDGET_BUTTON(DHMI_P5_TLB_3,VALUE='Start',XSIZE=OPT_BTN,EVENT_PRO='DHMI_DESERT_ARG_START')
  DHMI_P5_TLB_3_BTN   = WIDGET_BUTTON(DHMI_P5_TLB_3,VALUE='Exit',XSIZE=OPT_BTN, EVENT_PRO='DHMI_DESERT_ARG_EXIT')

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: COMPLETED DEFINING WIDGET'
  IF NOT KEYWORD_SET(GROUP_LEADER) THEN GROUP_LEADER = DHMI_P5_TLB
  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: STORING WIDGET INFO INTO STRUCTURE'
  DHMI_P5_INFO = {$
                  IVERBOSE              : IVERBOSE,$
                  GROUP_LEADER          : GROUP_LEADER,$
                  FSCP5_REGION          : FSCP5_REGION,$
                  ASITE                 : ASITES,$
                  NASITE                : NASITE,$
                  ISITE                 : 0,$
                  FSCP5_SENSOR          : FSCP5_SENSOR,$
                  ASENS                 : ASENS,$
                  NASENS                : NASENS,$
                  ISENS                 : 0,$
                  FSCP5_PROC            : FSCP5_PROC,$
                  APROC                 : APROC,$
                  NAPROC                : NAPROC,$
                  IPROC                 : 0,$
                  FSCP5_YEAR            : FSCP5_YEAR,$
                  AYEAR                 : AYEAR,$
                  NAYEAR                : NAYEAR,$
                  IYEAR                 : 0, $
                  FSCP5_CPU_NB         : FSCP5_CPU_NB, $
                  FSCP5_PHOTON_NB      : FSCP5_PHOTON_NB $
                  }
                  
;--------------------------
; REALISE THE WIDGET AND REGISTER WITH THE XMANAGER

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_DESERT_ARG: REALISING THE WIDGET AND REGISTERING WITH THE XMANAGER'
  WIDGET_CONTROL,DHMI_P5_TLB,/REALIZE,SET_UVALUE=DHMI_P5_INFO,/NO_COPY,GROUP_LEADER=GROUP_LEADER
  XMANAGER,'DHMI_DESERT_ARG',DHMI_P5_TLB

END
