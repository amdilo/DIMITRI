function GET_VIIRS_QUICKLOOK ,FILENAME, RGB=RGB, ROI=ROI, ICOORDS=ICOORDS, QL_QUALITY=QL_QUALITY, VERBOSE=VERBOSE
;+
; :Name:
;      GET_MODISA_QUICKLOOK
;
; :Description:
;      outputs a rgb modisa quicklook with roi overlay if requested
;
; :Calling sequence:
;      RES = GET_VIIRS_QUICKLOOK(FILENAME)
;
; :Params:
;      FILENAME :
;         A string containing the filename of the product for quicklook generation
;
; :Keywords:
;     RGB :
;       program generates an rgb colour quicklook (default is grayscale)
;     ROI :
;       Overlay coordinates of an roi in red (requires icoords)
;     ICOORDS :
;       A 4-element array of roi geolocation (n,s,e,w)
;     QL_QUALITY  :
;       Quality of jpeg generated (100 = max, 0 = lowest)
;     VERBOSE   :
;        Processing status outputs
;
; :Returns:
;     STATUS :
;       1: nominal, (-1) or 0: error
;       jpegs are automatically saved in filename folder
;
; :Common blocks:
;     NONE
;
; :History:
;      1 Jul 2013 - D Marrable
;-
  
  
;------------------------------------------------
; CHECK FILENAME IS NOMINAL

IF FILENAME EQ '' THEN BEGIN
  PRINT, 'VIIRS L1B QUICKLOOK: ERROR, INPUT FILENAME INCORRECT'
  RETURN,-1
ENDIF

;------------------------------------------------
; SET JPEG QUALITY IF NOT PROVIDED

IF N_ELEMENTS(QL_QUALITY) EQ 0 THEN QL_QUALITY = 100
IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: JPEG QUALITY = ',QL_QUALITY

;------------------------------------------------
; CHECK KEYWORD COMBINATIONS

IF KEYWORD_SET(ROI) AND N_ELEMENTS(ICOORDS) LT 4 THEN BEGIN
  PRINT,'VIIRS L1B QUICKLOOK: ERROR, ROI KEYWORD IS SET BUT COORDINATES INCORRECT'
  RETURN,-1
ENDIF

;------------------------------------------------
; DERIVE THE OUTPUT JPEG FILENAME

IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: COMPUTING OUTPUT FILENAME'
QL_POS = STRPOS(FILENAME,'.',/REVERSE_SEARCH)
QL_VIIRS_JPG = FILENAME
STRPUT,QL_VIIRS_JPG,'_',QL_POS
QL_VIIRS_JPG = STRING(QL_VIIRS_JPG+'.jpg')
ORDER=0

;------------------------------------------------
; GET VIIRS L1B RADIANCE DATA AND DERIVE QUICKLOOK IMAGE

IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: RETRIEVING RADIANCE DATA'
IF KEYWORD_SET(RGB) THEN BEGIN
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: RGB JPEG SELECTED'
  QL_DATAR = GET_VIIRS_L1B_RADIANCE(FILENAME, 2, /VERBOSE)
  QL_DATAG = GET_VIIRS_L1B_RADIANCE(FILENAME, 3, /VERBOSE)
  QL_DATAB = GET_VIIRS_L1B_RADIANCE(FILENAME, 4, /VERBOSE)
ENDIF ELSE BEGIN
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: GRAYSCALE SELECTED'
  QL_DATAR  = GET_VIIRS_L1B_RADIANCE(FILENAME, 3, /VERBOSE)
ENDELSE
  
IF QL_DATAR[0] EQ -1 THEN BEGIN
  PRINT,'VIIRS L1B QUICKLOOK: ERROR, PROBLEM ENCOUNTERED DURING L1B RADIANCE RETRIEVAL, CHECK PRODUCTS IS A VIIRSS L1B PRODUCT'
  RETURN,-1
ENDIF 
 
IF KEYWORD_SET(VERBOSE) THEN PRINT, 'MODISA L1B QUICKLOOK: GET PRODUCT DIMENSIONS AND DEFINE IMAGE ARRAY'
QL_DIMS  = SIZE(QL_DATAR)
QL_IMAGE = BYTARR(3,QL_DIMS[1],QL_DIMS[2])
  
;-----------------------------------------------
; POPULATE THE QUICKLOOK IMAGE ARRAY

IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: ADD RADIANCE DATA TO IMAGE ARRAY CHANNELS'
IF KEYWORD_SET(RGB) THEN BEGIN

  QL_IMAGE[0,*,*]=BYTSCL(QL_DATAR)
  QL_IMAGE[1,*,*]=BYTSCL(QL_DATAG)
  QL_IMAGE[2,*,*]=BYTSCL(QL_DATAB)
  
  ;----------------------------------------------
  ; RELEASE MEMORY FOR GREEN AND BLUE CHANNELS
  
  QL_DATAG = 0
  QL_DATAB = 0
  
ENDIF ELSE BEGIN

  QL_IMAGE[0,*,*]=BYTE(QL_DATAR)
  QL_IMAGE[1,*,*]=BYTE(QL_DATAR)
  QL_IMAGE[2,*,*]=BYTE(QL_DATAR)

ENDELSE 

;---------------------------------------------------
; GET LAT/LON DATA FOR ROI PIXEL INDEX IF REQUESTED

IF KEYWORD_SET(ROI) THEN BEGIN
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: OVERLAY OF ROI SELECTED'
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: RETRIEVING PRODUCT GEOLOCATION'
   QL_GEO = GET_VIIRS_LAT_LON(FILENAME)
  ; BECUASE WE HAVE FIXED THE BOWTIE EFFECT WE CAN'T GET LAT LON FROM THE ORIGINAL FILE NAME
  ; WE CACHED IT TO DISK IN GET_*_L1B_*.PRO

  RESTORE, 'LAT_LON_VIIRS_REGRID.SAV'
  QL_GEO = Temporary(BOWTIE_CORRECTION_STRUCTURE)

  
  ;---------------------------------------------------
  ; FIND LOCATION OF ROI
  
  QL_ROI_IDX = WHERE($
    QL_GEO.LAT LT ICOORDS[0] AND $
    QL_GEO.LAT GT ICOORDS[1] AND $
    QL_GEO.LON LT ICOORDS[2] AND $
    QL_GEO.LON GT ICOORDS[3] )
    
  ;---------------------------------------------------
  ; CONVERT ROI PIXELS TO A LIGHT RED OVERLAY
  
  IF QL_ROI_IDX[0] GT -1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: MODIFYING RED CHANNEL FOR ROI OVERLAY'
    ;IF KEYWORD_SET(RGB) THEN BEGIN
    ;QL_DATAR = BYTE(QL_DATAR)
    QL_DATAR = QL_IMAGE[0,*,*]
    QL_DATAG = QL_IMAGE[1,*,*]
    QL_DATAB = QL_IMAGE[2,*,*]
    ;ENDIF ELSE QL_DATAR = BYTSCL(QL_DATAR,MAX=TEMP_MAX)
    
    QL_DATAR[QL_ROI_IDX]=250
    QL_DATAG[QL_ROI_IDX]=bytscl(QL_DATAG[QL_ROI_IDX],top=75)
    QL_DATAB[QL_ROI_IDX]=bytscl(QL_DATAb[QL_ROI_IDX],top=75)
    QL_IMAGE[0,*,*] = QL_DATAR
    QL_IMAGE[1,*,*] = QL_DATAG
    QL_IMAGE[2,*,*] = QL_DATAB
    
    
  ENDIF
  
  IF (QL_GEO.LAT[2,QL_DIMS[2]-3]) GT (QL_GEO.LAT[2,2]) THEN ORDER=1 ELSE ORDER=0 
  
ENDIF

;---------------------------------------------------
; OUTPUT QUICKLOOK AS JPEG

IF KEYWORD_SET(VERBOSE) THEN PRINT, 'VIIRS L1B QUICKLOOK: WRITING IMAGE TO JPEG'
WRITE_JPEG ,QL_VIIRS_JPG,REVERSE(REVERSE(QL_IMAGE,3),2),TRUE=1,ORDER=ORDER

;---------------------------------------------------
; RETURN A POSITIVE VALUE INDICATING QL GENERATION OK

RETURN, 1
  
END

  
  