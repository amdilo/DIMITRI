;-----------------------------------------
; NAME:
;  SELECT_BEST_AEROSOL
; -------------------------
; PURPOSE:
;    LOOKING FOR BEST AEROSOL TYPE AND TOA THAT WERE OBTAINED DURING THE BRDF INVERSION  
; 
; -------------------------
;CALLING SEQUENCE:
;      RES =SELECT_BEST_AEROSOL(SENSOR, SITE_NAME, /DISPLAY_PLOT )
;
; -------------------------
; INPUT:
; -SENSOR           - THE NAME OF THE SENSOR E.G. 'PARASOL'
; -SITE_NAME        - THE VALIDATION SITE NAME E.G. 'LIBYA4'
;
; --------------------------------
;  KEYWORDS:
;    /DISPLAY_PLOT : MAKES A PLOT OF THE AEROSOL AND TOA OBTAINED DURING THE BRDF INVERSION 
;
; --------------------------------
; OUTPUTS:
; - A STRING FOR BEST AEROSOL TYPE AND TOA THAT WERE OBTAINED DURING THE BRDF INVERSION
;
; -------------------------
; EXAMPLE:
;
; -------------------------
; TROUBLE SHOOTING:
;
; -------------------------
;COMMON BLOCKS:
;      NON
;
; -------------------------
; MODIFICATION HISTORY:
;        18 NOV 2012  - M BOUVET    - FIRST PROTOTYPE OF THE ROUTINE
;        15 JAN 2015  - B ALHAMMOUD - FIRST IMPLEMENTATION TO DIMITRI-V3.1A
;
; VALIDATION HISTORY:
;        21 JAN 2015 -  B ALHAMMOUD - LINUX 64-BIT MACHINE IDL 8.2, NOMINAL COMPILATION AND OPERATION.
;                                  TESTED FOR PARASOL OVER LIBYA4
;
; -------------------------

FUNCTION SELECT_BEST_AEROSOL, SENSOR, SITE_NAME, DISPLAY_PLOT=DISPLAY_PLOT

; LOOK FOR ALL INVERSION RESULT FOR BAND 1
FILES=FILE_SEARCH('./Reference_dataset/'+SENSOR+'/'+SITE_NAME,'Inverted_RPV_parameters_1_*_full_RSR.sav')

; DEFINE VARIABLES WHERE INVERSION INFORMATION WILL BE STORED
INVERSION_AEROSOL_TYPE=STRARR(N_ELEMENTS(FILES))
INVERSION_AEROSOL_AOT=STRARR(N_ELEMENTS(FILES))
INVERSION_CHI_SQUARE_MIN=FLTARR(N_ELEMENTS(FILES))
INVERSION_CHI_SQUARE_STDDEV=FLTARR(N_ELEMENTS(FILES))

; PREPARE PLOT
IF KEYWORD_SET(DISPLAY_PLOT) THEN BEGIN
  SET_PLOT, 'X'
  WINDOW, 10,RETAIN=2, XSIZE=1200, YSIZE=900
  DEVICE, DECOMPOSED=0
ENDIF ELSE BEGIN
  MACHINE_WINDOW = !D.NAME
  SET_PLOT, 'Z'
  DEVICE, DECOMPOSED=0, SET_RESOLUTION=[1200, 900],SET_PIXEL_DEPTH=24
ENDELSE
!P.MULTI=0
LOADCT, 39


; PRINT ALL RELEVANT INFO EXTRACTED
PRINT, '-------------------------------'
PRINT, 'SENSOR:', SENSOR
PRINT, 'SITE  :', SITE_NAME


;;;;;;;;;;;;;;
; EXTRACT THE VALUES OF THE OPTIMIZATION PROCESS FOR EACH AEROSOL TYPE AND AOT
;;;;;;;;;;;;;;
FOR I_FILE=0, N_ELEMENTS(FILES)-1 DO BEGIN
  ; EXTRACT THE AEROSOL TYPE AND AOT CORRESPONDING TO EACH SIMULATION DONE IN THE BAND 1
  START_POSITION_FOR_AEROSOL_INFO=STRPOS(FILES[I_FILE], '_full_RSR')
  OFFSET_START_AOT=STRPOS(FILES[I_FILE],'_', START_POSITION_FOR_AEROSOL_INFO-1,  /REVERSE_SEARCH)
  OFFSET_END_AOT=START_POSITION_FOR_AEROSOL_INFO
  INVERSION_AEROSOL_AOT[I_FILE]=STRMID(FILES[I_FILE], OFFSET_START_AOT+1, OFFSET_END_AOT-OFFSET_START_AOT-1)
  OFFSET_START_TYPE=STRPOS(FILES[I_FILE],'1', OFFSET_START_AOT-1,  /REVERSE_SEARCH)+1
  OFFSET_END_TYPE=OFFSET_START_AOT
  INVERSION_AEROSOL_TYPE[I_FILE]=STRMID(FILES[I_FILE], OFFSET_START_TYPE+1, OFFSET_END_TYPE-OFFSET_START_TYPE-1)
  ; RETRIEVE THE CORRESPONDING CHI_SQUARE
  RESTORE, FILES[I_FILE], VERBOSE=VERBOSE
  INVERSION_CHI_SQUARE_MIN[I_FILE]=MIN(FINAL_RMSE[WHERE(FINAL_INVERTED_PARAMETERS[0,*] NE -999)])
  INVERSION_CHI_SQUARE_STDDEV[I_FILE]=STDDEV(FINAL_RMSE[WHERE(FINAL_INVERTED_PARAMETERS[0,*] NE -999)])
  PRINT, '-------------------------------'  
  PRINT, 'Aerosol type: ', INVERSION_AEROSOL_TYPE[I_FILE]
  PRINT, 'AOT 550     : ', INVERSION_AEROSOL_AOT[I_FILE]
  PRINT, 'Band 1 RMSE for each rho_0 seed: ', FINAL_RMSE
  PRINT, 'Convergence for each rho_0 seed (1=yes, 0=no): ', TRANSPOSE(FLOAT(FINAL_INVERTED_PARAMETERS[0,*] NE -999))
  PRINT, 'Best RMSE in band 1 :', INVERSION_CHI_SQUARE_MIN[I_FILE]
ENDFOR


; PLOT
AERO_TYPE=INVERSION_AEROSOL_TYPE(UNIQ(INVERSION_AEROSOL_TYPE))

FOR I_TYPE=0, N_ELEMENTS(AERO_TYPE)-1 DO BEGIN
  IF I_TYPE EQ 0 THEN PLOT,  INVERSION_AEROSOL_AOT[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])], INVERSION_CHI_SQUARE_MIN[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])], PSYM=2, CHARSIZE=2, COLOR=0, BACKGROUND=255, XTITLE='AOT', YTITLE='RMSE in % after optimisation in band 1', YRANGE=[0,5], TITLE=SENSOR+' / '+SITE_NAME 
  OPLOT,  INVERSION_AEROSOL_AOT[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])], INVERSION_CHI_SQUARE_MIN[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])], COLOR=250./N_ELEMENTS(AERO_TYPE)*(1+I_TYPE), PSYM=2
  
  ; JUST A TRICK TO ALLOW PLOTTING TO WORK FOR THE FIRST POINT
  AOT_VALUES=INVERSION_AEROSOL_AOT[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])]
  AOT_VALUES[0]=0.0001
  ; PLOT THE STDDEV ASSOCIATED TO THE RMSE OBTAINED FOR THE DIFFERENT RUNS WITH CHANGING RHO_0 INITIAL VALUES
  OPLOTERROR,AOT_VALUES,INVERSION_CHI_SQUARE_MIN[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])]  , INVERSION_CHI_SQUARE_STDDEV[WHERE(INVERSION_AEROSOL_TYPE EQ AERO_TYPE[I_TYPE])]/2. , COLOR=250./N_ELEMENTS(AERO_TYPE)*(1+I_TYPE), PSYM=2, ERRCOLOR=250./N_ELEMENTS(AERO_TYPE)*(1+I_TYPE), ERRSTYLE=0
  XYOUTS, 800, 800-I_TYPE*20, '* '+AERO_TYPE[I_TYPE],/DEVICE, COLOR=250./N_ELEMENTS(AERO_TYPE)*(1+I_TYPE), CHARSIZE=2

ENDFOR
WRITE_JPEG,'./Reference_dataset/'+SENSOR+'/'+SITE_NAME+'/Aerosol_optimisation_results'+SITE_NAME+'.jpeg',TVRD(TRUE=3), TRUE=3, QUALITY=100


; PRINT THE BEST AEROSOL MODEL AND AOT
PRINT, '-------------------------------'
PRINT, 'Best overall inversion RMSE: ', MIN(INVERSION_CHI_SQUARE_MIN, MIN_INDEX)
PRINT, 'Best overal aero type: ', INVERSION_AEROSOL_TYPE[MIN_INDEX]
PRINT, 'Best overal AOT 550: ', INVERSION_AEROSOL_AOT[MIN_INDEX]
PRINT, '-------------------------------'
PRINT, '-------------------------------'


; SELECT THE AOT/AERO TYPE WHICH DELIVERS THE SMALLEST RMSE
BEST_AERO=STRARR(2)
BEST_AERO[0]=INVERSION_AEROSOL_TYPE[MIN_INDEX]
BEST_AERO[1]=INVERSION_AEROSOL_AOT[MIN_INDEX]


RETURN, BEST_AERO

END