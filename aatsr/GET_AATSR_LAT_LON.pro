;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_AATSR_LAT_LON       
;* 
;* PURPOSE:
;*      RETURNS THE LATITUDE AND LONGITUDE OF A AATSR IMAGE
;* 
;* CALLING SEQUENCE:
;*      RES = GET_AATSR_LAT_LON(FILENAME,GEO_DIR)      
;* 
;* INPUTS:
;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR GEOLOCATION EXTRACTION
;*      GEO_DIR  - A STRING OF THE VEWING DIRECTION REQUIRED: 'NADIR' OR 'FWARD'      
;*
;* KEYWORDS:
;*      VERBOSE  - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      STRUCT.LATITUDE   - LATITUDE IN DEGREES FOR L1B PRODUCT
;*      STRUCT.LONGITUDE  - LONGITUDE IN DEGREES FOR L1B PRODUCT	
;*      NOTE, THE DATA AT POINT [0,0] IS EQUAL TO THE TOP RIGHT PIXEL IN BEAM. 
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      04 APR 2005 - M BOUVET - PROTOTYPE DIMITRI VERSION
;*      12 DEC 2010 - C KENT   - DIMITRI-2 V1.0
;*      14 DEC 2010 - C KENT   - UPDATED HEADER INFORMATION 
;*      12 JUL 2011 - C KENT   - FIXED LAT/LON EXTRACTION BUG
;*
;* VALIDATION HISTORY:
;*      13 DEC 2010 - C KENT    - WINDOWS 32-BIT MACHINE IDL 7.1: COMPILATION SUCCESSFUL,
;*                                SLIGHT DIFFERENCES TO BEAM VISAT POSSIBLY DUE TO INTERPOLATION TECHNIQUES
;*      06 JAN 2010 - C KENT    - LINUX 64-BIT MACHINE IDL 8.0: COMPILATION SUCCESSFUL,
;*                                VALUES EQUAL TO WINDOWS 32-BIT MACHINE
;*
;**************************************************************************************
;**************************************************************************************
;NOTE, NEEDS ENVISAT AND AATSR TO BE COMPILED

FUNCTION GET_AATSR_LAT_LON,FILENAME,GEO_DIR,LENGTH,WIDTH,VERBOSE=VERBOSE

;------------------------------------------------
; CHECK FILENAME IS NOMINAL

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'AATSR L1B LAT LON: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF

;------------------------------------------------
; OPEN THE PRODUCT AND READ THE HEADER INFORMATION
 
  OPENR, GEO_LUN, FILENAME, /GET_LUN, /SWAP_IF_LITTLE_ENDIAN
  AATSR_GET_HEADERS, GEO_LUN, GEO_MPH, GEO_SPH, GEO_DSD

;------------------------------------------------
; RETRIEVE ADS DATA DEPENDING ON DIRECTION

  CASE GEO_DIR OF
    'NADIR': TEMP = AATSR_READ_ADS_LOC(GEO_LUN, GEO_DSD[1],GEO_SPH,0,LENGTH,0,WIDTH,/NADIR_CORRECTION)
    'FWARD': TEMP = AATSR_READ_ADS_LOC(GEO_LUN, GEO_DSD[1],GEO_SPH,0,LENGTH,0,WIDTH,/FORWARD_CORRECTION)
  ELSE :BEGIN 
    PRINT, 'AATSR L1B LAT LON: ERROR, SUPPLIED DIRECTION INCORRECT'
    RETURN,{ERROR:-1} 
  END
  ENDCASE

;------------------------------------------------
; CLOSE THE PRODUCT AND FREE THE LUN

  CLOSE,GEO_LUN
  FREE_LUN, GEO_LUN

;------------------------------------------------
; RETURN GEOLOCATIO INFORMATION

  RETURN,{LAT:TEMP.LATITUDE,LON:TEMP.LONGITUDE}

END 