;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_SITE_NAME       
;* 
;* PURPOSE:
;*      RETURNS AN ARRAY OF FILTERS FOR SEARCHING FOR DATA PRODUCTS
;* 
;* CALLING SEQUENCE:
;*      RES = GET_SITE_NAME(SITE_TYPE=SITE_TYPE,VERBOSE=VERBOSE)      
;* 
;* INPUTS:
;*      NONE
;*
;* KEYWORDS:
;*      VERBOSE  - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      SITE_NAMES  - AN ARRAY OF THE REQUESTED SITE NAMES
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      29 NOV 2013 - PML / MAGELLIUM - DIMITRI-3 MAG
;*      26 MAR 2015 - NCG / MAGELLIUM - REMOVE SITE_FILE PARAMETER AND ADD SITE_TYPE KEYWORD (DIMITRI V4)
;*
;* VALIDATION HISTORY:
;*      20 JAN 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL 
;*      30 MAR 2015 - NCG / MAGELLIUM      - WINDOWS 64BIT MACHINE IDL 8.0: COMPILATION AND OPERATION SUCCESSFUL (DIMITRI V4.0) 
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_SITE_NAME,SITE_TYPE=SITE_TYPE,VERBOSE=VERBOSE

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'SITE_NAMES: RETRIEVING SITE NAMES '

;-----------------------------
; CHECK FILE EXISTS

  SITE_FILE = GET_DIMITRI_LOCATION('SITE_DATA')
  TEMP = FILE_INFO(SITE_FILE)
  IF TEMP.EXISTS EQ 0 THEN BEGIN
    PRINT, 'SITE_COORDINATES: ERROR, SITE FILE DOES NOT EXIST'
    RETURN,-1
  ENDIF

;-----------------------------
; GET TEMPLATE FOR SITE FILE

  IF KEYWORD_SET(VERBOSE) THEN SITE_TEMPLATE = GET_DIMITRI_SITE_DATA_TEMPLATE(/VERBOSE) $
  ELSE SITE_TEMPLATE = GET_DIMITRI_SITE_DATA_TEMPLATE()

;-----------------------------
; READ FILE
  
  SITE_DATA = READ_ASCII(SITE_FILE,TEMPLATE=SITE_TEMPLATE)
  IF N_ELEMENTS(SITE_DATA.SITE_ID) EQ 0 THEN BEGIN
    PRINT,'SITE_NAMES: ERROR, SITE_ID ERROR IN SITE_FILE' 
    RETURN,-1
  ENDIF

;-----------------------------
; CHECK SITE_TYPE KEYWORD
  
  IF KEYWORD_SET(SITE_TYPE) THEN BEGIN
    
    IDX = WHERE( STRCMP(SITE_DATA.TYPE, SITE_TYPE, /FOLD_CASE) EQ 1, COUNT)
    IF COUNT EQ 0 THEN RETURN, -1 $
                  ELSE RETURN, SITE_DATA.SITE_ID[IDX]
  
  ENDIF

;-----------------------------
; RETURN SITE NAMES
  
  RETURN,SITE_DATA.SITE_ID

END

