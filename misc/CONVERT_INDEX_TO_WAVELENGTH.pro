;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      CONVERT_INDEX_TO_WAVELENGTH       
;* 
;* PURPOSE:
;*      RETURNS THE DIMITRI WAVELENGTH FOR A GIVEN SENSOR AND BAND INDEX. NOTE, 
;*      DIMITRI BAND INDEXES RANGE START FROM 1, SENSOR INDEXES START FROM 0.
;* 
;* CALLING SEQUENCE:
;*      RES = CONVERT_INDEX_TO_WAVELENGTH(IDX,IW_SENSOR)      
;* 
;* INPUTS:
;*      IDX       - AN INTEGER OF THE SENSOR BAND INDEX (E.G. MERIS 0 = 412NM)
;*      IW_SENSOR - A STRING OF THE SENSOR NAME (E.G. 'MERIS')
;*
;* KEYWORDS:
;*      VERBOSE   - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      ITW_VALUE - A STRING OF THE DIMITRI WAVELENGTH FOR GIVEN SENSOR AND BAND INDEX
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      19 JAN 2011 - C KENT   - DIMITRI-2 V1.0
;*      21 MAR 2011 - C KENT   - MODIFIED FILE DEFINITION TO USE GET_DIMITRI_LOCATION
;*
;* VALIDATION HISTORY:
;*      14 APR 2011 - C KENT   - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                               COMPILATION AND OPERATION 
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION CONVERT_INDEX_TO_WAVELENGTH,IDX,IW_SENSOR,VERBOSE=VERBOSE

;------------------------------------
; DEFINE SENSOR FILE
  
  SBI_FILE = GET_DIMITRI_LOCATION('BAND_INDEX')
  RES = FILE_INFO(SBI_FILE)
  IF RES.EXISTS EQ 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'INDEX TO WAVELENGTH: ERROR, SENSOR INFORMATION FILE NOT FOUND'
    RETURN,'ERROR'
  ENDIF

;------------------------------------
; RETRIEVE TEMPLATE AND READ DATA FILE  
  
  TEMP = GET_DIMITRI_BAND_INDEX_TEMPLATE()
  BI_DATA = READ_ASCII(SBI_FILE,TEMPLATE=TEMP)

;------------------------------------
; FIND INDEX OF INPUT SENSOR

  RES = WHERE(STRCMP(TEMP.FIELDNAMES,IW_SENSOR) EQ 1)
  IF RES[0] EQ -1 OR N_ELEMENTS(RES) GT 1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'INDEX TO WAVELENGTH: ERROR, SENSOR INDEX RETRIEVAL'
    RETURN,'ERROR'  
  ENDIF

;------------------------------------
; FIND INDEX OF INPUT INDEX AND RETURN 
; THE ASSOCIATED WAVECENTRE  

  TEMP = WHERE(BI_DATA.(RES) EQ IDX[0])
  IF TEMP[0] EQ -1 OR N_ELEMENTS(TEMP) GT 1 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'INDEX TO WAVELENGTH: ERROR, CANNOT FIND WAVELENGTH ID'
    RETURN,'ERROR'  
  ENDIF  
  
  ITW_VALUE = BI_DATA.(1)[TEMP]
  ITW_VALUE = STRTRIM(STRING(FIX(ITW_VALUE)),2)
  RETURN,ITW_VALUE

END