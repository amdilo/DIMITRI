;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      CLOUD_MODULE_GLOBCARBON       
;* 
;* PURPOSE:
;*      THIS FUNCTION PERFORMS THE CLOUDSCREENING ALGORITHM DEVELOPED FOR THE GLOBCARBON 
;*      PROJECT. THE ALGORITHM PERFORMS 2 CLOUD SCREENING PASSES ON PIXELS FLAGGED AS 
;*      "BRIGHT" BY THE OPERATIONAL MERIS BRIGHT PIXEL FLAG ALGORITHM. tHIS ALGORITHM 
;*      HAS BEEN MODIFIED TO WORK USING FIXED THRESHOLDS.
;* 
;* CALLING SEQUENCE:
;*      RES = CLOUD_MODULE_GLOBCARBON(SITE_TYPE,GCARBON_REF,CS_ANG)    
;* 
;* INPUTS:
;*      SITE_TYPE  - A STRING OF THE DIMITRI VALIDATION SITE TYPE (E.G. 'OCEANIC')'
;*      GCARBON_REF- A FLOAT ARRAY CONTAINING THE TOA REFLECTANCE AT 443,705,750 AND 
;*                   865NM [NB_PIXELS,RHO_BANDS]
;*      CS_ANG    - A 3 OR 4 ELEMENT ARRAY CONTAINING THE SZA AND VZA VALUES AND EITHER
;*                   RAA OR SAA AND VAA 
;*
;* KEYWORDS:
;*      VERBOSE    - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      CLASS_PIXELS - AN INTEGER ARRAY OF SIXE NUMBER OF PIXELS, 0 MEANS CLEAR PIXEL, 
;*                     1 MEANS CLOUDY
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      06 APR 2011 - C KENT   - DIMITRI-2 V1.0
;*      07 APR 2011 - C KENT   - ADDED BAND INDEX VALUES
;*
;* VALIDATION HISTORY:
;*      12 APR 2011 - C KENT   - NOMINAL COMPILATION AND OPERATION ON WINDOWS 32BIT 
;*                               IDL 7.1 AND LINUX 64BIT IDL 8.0 
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION CLOUD_MODULE_GLOBCARBON,SITE_TYPE,GCARBON_REF,CS_ANG,CS_CLASSIF_MATRIX=CS_CLASSIF_MATRIX,VERBOSE=VERBOSE


  STATUS_OK = GET_DIMITRI_LOCATION('STATUS_OK')
  STATUS_ERROR = GET_DIMITRI_LOCATION('STATUS_ERROR')

  MISSING_VALUE_LONG=LONG(GET_DIMITRI_LOCATION('NCDF_MISSING_VALUE'))

;-------------------------
; RESTORE BRIGHT LUT INFORMATION

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON: RESTORING BRIGHT LUT INFORMATION'
  TEMP_LUT = GET_DIMITRI_LOCATION('BRIGHT_LUT')
  RESTORE,TEMP_LUT

;-------------------------
; DEFINE SNOW AND CLOUD THRESHOLDS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON: DEFINING THRESHOLDS AND PARAMETERS'
  SNOW_THRESH   = 1.0
  CLOUD_THRESH  = 0.8

;-------------------------
; DEFINE PARAMETERS FOR CLOUD SCREENING

  ICLEAR  = 0
  ICLOUD  = 1
  ISNOW   = 2
  IBRIGHT = 3
  
  TEMP_DIMS     = SIZE(GCARBON_REF,/DIMENSIONS)
  NB_PIXELS     = TEMP_DIMS[0]
  BRIGHT_PIXELS = INTARR(NB_PIXELS)
  CLASS_PIXELS  = MAKE_ARRAY(NB_PIXELS,/INTEGER,VALUE=ICLEAR)

  B1 = 0
  B2 = 1
  B3 = 2
  B4 = 3

;-----------------------
; SET OUTPUT STRUCTURE
  TEMP_DIMS     = SIZE(GCARBON_REF,/DIMENSIONS)
  NBCAN=TEMP_DIMS[1]

  CS_CLASSIF_MATRIX= MAKE_ARRAY(NB_PIXELS,2,/LONG,VALUE=MISSING_VALUE_LONG)
  IDX_VALID  = INDGEN(NB_PIXELS)

  CS_CLASSIF_MATRIX[*,0]=IDX_VALID

;-------------------------
; FOR EACH PIXEL FIND OUT IF IT'S "BRIGHT"

  TOA_GEO = MAKE_ARRAY(NB_PIXELS,4,/FLOAT,VALUE=MISSING_VALUE_FLT)
  TOA_GEO[*,0] = CS_ANG[*,0] ; SZA
  TOA_GEO[*,1] = CS_ANG[*,2] ; VZA
  TOA_GEO[*,2] = CS_ANG[*,1] ; SAA
  TOA_GEO[*,3] = CS_ANG[*,3] ; VAA
  FOR GCI=0L,NB_PIXELS-1 DO BRIGHT_PIXELS[GCI] = DIMITRI_CLOUD_SCREENING_BRIGHT_RHO(GCARBON_REF[GCI,B1],TOA_GEO[GCI,*],BRIGHT_RHO)
  
;-------------------------
; IF NO BRIGHT PIXELS THEN RETURN ALL PIXELS AS CLEAR

  BRIGHTF = WHERE(BRIGHT_PIXELS EQ 1, COUNT_BRIGHT)
  IF COUNT_BRIGHT EQ 0 THEN BEGIN
    CS_CLASSIF_MATRIX[*,1] = ICLEAR
    RETURN, STATUS_OK
  ENDIF
  
;-------------------------
; IF OCEANIC SITE THEN BRIGHT PIXELS ARE CLOUD

  IF SITE_TYPE EQ 'OCEANIC' THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON: OCEANIC EXCEPTION, BRIGHT PIXELS ARE CLOUD'
    CLASS_PIXELS[BRIGHTF] = ICLOUD
    CS_CLASSIF_MATRIX[*,1]=CLASS_PIXELS
    RETURN, STATUS_OK
  ENDIF ELSE CLASS_PIXELS[BRIGHTF] =  IBRIGHT

;-------------------------
; PASS 1: PERFORM RATIO OF B9/B13 
; TO DEFINE EITHER SNOW OR CLOUD PIXELS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON: PERFORMING PASS 1'
  PASS_1 = GCARBON_REF[*,B2]/GCARBON_REF[*,B4]
  SNOWF = WHERE(PASS_1 GT SNOW_THRESH AND CLASS_PIXELS EQ IBRIGHT)
  IF SNOWF[0] GT -1 THEN CLASS_PIXELS[SNOWF] = ISNOW 

;-------------------------
; PASS 2: OF PIXELS FLAGGED AS 
; CLOUD USE B2/B10 TO DETERMINE CLOUD OR BRIGHT LAND

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON: PERFORMING PASS 2'
  PASS_2 = GCARBON_REF[*,B1]/GCARBON_REF[*,B3]
  CLOUDF = WHERE(PASS_2 GT CLOUD_THRESH AND CLASS_PIXELS NE ISNOW AND CLASS_PIXELS EQ IBRIGHT)
  IF CLOUDF[0] GT -1 THEN CLASS_PIXELS[CLOUDF] = ICLOUD 

;-------------------------
; COMPUTE STATS FOR PRODUCT

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'CLOUD_MODULE_GLOBCARBON: COMPUTING STATS'
  STATS_SNOW  = 100*FLOAT(N_ELEMENTS(SNOWF))/FLOAT(NB_PIXELS)
  STATS_CLOUD = 100*FLOAT(N_ELEMENTS(CLOUDF))/FLOAT(NB_PIXELS)
  STATS_CLEAR = 100.0-STATS_SNOW-STATS_CLOUD
 
  IF KEYWORD_SET(VERBOSE) THEN PRINT,$
    'CLOUD_MODULE_GLOBCARBON: SNOW: ',STATS_SNOW,' CLOUD: ',STATS_CLOUD,' CLEAR: ',STATS_CLEAR

;-------------------------
; RESET ALL SNOW PIXELS AS CLEAR

  IF SNOWF[0] GT -1 THEN CLASS_PIXELS[SNOWF] = ICLEAR

;-------------------------
; RESET ALL BRIGHT PIXELS AS CLEAR
  
  RES = WHERE(CLASS_PIXELS EQ IBRIGHT)
  IF RES[0] GT -1 THEN CLASS_PIXELS[RES] = ICLEAR  
    
;-------------------------
; UPDATE OUTPUT STRUCTURE
  CS_CLASSIF_MATRIX[*,1]=CLASS_PIXELS

  RETURN, STATUS_OK

END

