FUNCTION DISTANCE_FROM_LAND,PIXEL_LAT,PIXEL_LON

;*****************************************************************************
;* THIS FUNCTION COMPUTES DISTANCE FROM LAND A PIXEL IS GIVEN IT'S GEOLOCATION
;*
;* INPUTS:
;*    PIXEL_LAT - THE PIXELS LATITUDE
;*    PIXEL_LON - THE PIXELS LONGITUDE
;*
;* VERISON: 
;*    1.0 : C. KENT. ARGANS LTD, JUNE 2011 
;*
;* NOTES:
;*    VALID DISTANCES ARE FROM 0 (LAND) TO 50 (KM FROM LAND), 999 MEANS 
;*    IT'S GREATER THAN 50 KM, AND -1 MEANS AN INTERNAL ERROR HAS OCCURED
;****************************************************************************

;---------------------------
; LANDMASK DATA IS A STRUCTURE OF DST,LATS,LONS

COMMON LANDMASK, LANDMASK_DATA 

;---------------------------
; DEFINE PARAMETERS

  DST_TOL   = 0.05 ;OF A DEGREE, VERY VERY VERY APPROXIMATELY 5KM
  SLOOP     = 0
  ELOOP     = 10
  FLTOL     = 0.000001
  MAX_VAL   = 50
  FIL_VAL   = 999
  BAD_VAL   = -1
  DST_EX    = 0

;---------------------------
; START LOOP

  FOR LMI = SLOOP,ELOOP DO BEGIN 

;---------------------------
; SEARCH FOR LATS WITHIN RANGE

    LMTOL = DST_TOL*FLOAT(LMI)
    LAT_ID = WHERE(LANDMASK_DATA.LATS GT PIXEL_LAT-LMTOL AND LANDMASK_DATA.LATS LT PIXEL_LAT+LMTOL,LATCOUNT)
    IF LATCOUNT EQ 0 THEN CONTINUE

;---------------------------
;  IF FOUND SOME THEN FIND LONS WITHIN RANGE

    GOOD_LATS = LANDMASK_DATA.LATS[LAT_ID]
    LON_ID = WHERE(LANDMASK_DATA.LONS[LAT_ID] GT PIXEL_LON-LMTOL AND LANDMASK_DATA.LONS[LAT_ID] LT PIXEL_LON+LMTOL,LONCOUNT)  
    IF LONCOUNT EQ 0 THEN CONTINUE

;---------------------------
; IF LONS FOUND THEN FIND WHERE THE DISTANCE FROM OUR POINT IS THE SMALLEST

    GOOD_LONS = LANDMASK_DATA.LONS[LAT_ID[LON_ID]]
    GOOD_LATS = GOOD_LATS[LON_ID]

    GEO_DISTANCE = SQRT((GOOD_LONS-PIXEL_LON)^2+(GOOD_LATS-PIXEL_LAT)^2)
    GEO_MATCH_ID = WHERE(GEO_DISTANCE-MIN(GEO_DISTANCE) LT FLTOL)

    MATCH_LAT = GOOD_LATS[GEO_MATCH_ID[0]]
    MATCH_LON = GOOD_LONS[GEO_MATCH_ID[0]]
    DST_EX = 1
  
  ENDFOR

;---------------------------
; MAKE SURE THAT WE'VE FOUND SOMETHING

  IF DST_EX THEN BEGIN
  
;---------------------------
; GET ID'S IN MAIN LANDMASK DATASET

    PY_ID = WHERE(LANDMASK_DATA.LATS-MATCH_LAT LT FLTOL)
    PX_ID = WHERE(LANDMASK_DATA.LONS-MATCH_LON LT FLTOL)

;---------------------------
; GET CORRESPONDING DST VALUE

    DFL = LANDMASK_DATA.DST[PX_ID[0],PY_ID[0]]
    IF DFL GT MAX_VAL THEN DFL = FIL_VAL ELSE DFL = FIX(DFL)

  ENDIF ELSE DFL = BAD_VAL

;---------------------------
; RETURN DISTANCE FROM LAND

  RETURN,DFL

END