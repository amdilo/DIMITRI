;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      GET_PARASOL_QUICKLOOK       
;* 
;* PURPOSE:
;*      OUTPUTS A RGB/GRAYSCALE PARASOL QUICKLOOK WITH ROI OVERLAY IF REQUESTED
;* 
;* CALLING SEQUENCE:
;*      RES = GET_PARASOL_QUICKLOOK(FILENAME)      
;* 
;* INPUTS:
;*      FILENAME - A SCALAR CONTAINING THE FILENAME OF THE PRODUCT FOR QUICKLOOK GENERATION 
;*
;* KEYWORDS:
;*     RGB          -  PROGRAM GENERATES AN RGB COLOUR QUICKLOOK (DEFAULT IS GRAYSCALE)
;*     ROI          -  OVERLAY COORDINATES OF AN ROI IN RED (REQUIRES ICOORDS)
;*     ICOORDS      -  A 4-ELEMENT ARRAY OF ROI GEOLOCATION (N,S,E,W) 
;*     QL_QUALITY   -  QUALITY OF JPEG GENERATED (100 = MAX, 0 = LOWEST)
;*     ENDIAN_SIZE  -  MACHINE ENDIAN SIZE (0: LITTLE, 1: BIG)
;*     VERBOSE      -  PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*     STATUS       - 1: NOMINAL, (-1) OR 0: ERROR
;*     JPESG ARE AUTOMATICALLY SAVED IN FILENAME FOLDER    
;*
;* COMMON BLOCKS:
;*     NONE 
;*
;* MODIFICATION HISTORY:
;*                  - M BOUVET - PROTOTYPE DIMITRI VERSION
;*      17 DEC 2010 - C KENT   - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      01 DEC 2010 - C KENT    - WINDOWS 32-BIT MACHINE: COMPILATION SUCCESSFUL,
;*                                RGB,ROI,ICOORDS AND QL_QUALITY KEYWORDS TESTED 
;*                                SUCCESSFULLY USING UYUNI COORDINATES
;*                              - LINUX 64-BIT MACHINE, IDL 8.0: COMPILATION SUCESSFUL, 
;*                                RESULTS EQUAL TO WINDOWS MACHINE
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION GET_PARASOL_QUICKLOOK,FILENAME,RGB=RGB,ROI=ROI,ICOORDS=ICOORDS,QL_QUALITY=QL_QUALITY,ENDIAN_SIZE=ENDIAN_SIZE,VERBOSE=VERBOSE

;------------------------------------------------
; CHECK FILENAME AND IN_BAND ARE NOMINAL

  IF FILENAME EQ '' THEN BEGIN
    PRINT, 'PARASOL L1B QUICKLOOK: ERROR, INPUT FILENAME INCORRECT'
    RETURN,-1
  ENDIF

;------------------------------------------------
; IF ENDIAN SIZE NOT PROVIDED THEN GET VALUE

  IF N_ELEMENTS(ENDIAN_SIZE) EQ 0 THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN BEGIN
      PRINT, 'PARASOL L1B QUICKLOOK: NO ENDIAN SIZE PROVIDED, RETRIEVING...'
      ENDIAN_SIZE = GET_ENDIAN_SIZE(/VERBOSE)
    ENDIF ELSE ENDIAN_SIZE = GET_ENDIAN_SIZE()
  ENDIF
  
;------------------------------------------------
; SET JPEG QUALITY IF NOT PROVIDED

  IF N_ELEMENTS(QL_QUALITY) EQ 0 THEN QL_QUALITY = 90
  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: JPEG QUALITY = ',QL_QUALITY
  
;------------------------------------------------
; CHECK KEYWORD COMBINATIONS

  IF KEYWORD_SET(ROI) AND N_ELEMENTS(ICOORDS) LT 4 THEN BEGIN
    PRINT,'PARASOL L1B QUICKLOOK: ERROR, ROI KEYWORD IS SET BUT COORDINATES INCORRECT'
    RETURN,-1
  ENDIF

;------------------------------------------------
; DERIVE THE OUTPUT JPEG FILENAME

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: COMPUTING OUTPUT FILENAME'
  QL_PARASOL_JPG = FILENAME
  QL_PARASOL_JPG = STRING(filename+'.jpg')

;------------------------------------------------
; GET PARASOL L1B RADIANCE DATA AND DERIVE QUICKLOOK IMAGE

  IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT, 'PARASOL L1B QUICKLOOK: RETRIEVING RGB RADIANCE DATA'
    L1B_DATA = GET_PARASOL_L1B_DATA(FILENAME,ENDIAN_SIZE=ENDIAN_SIZE,/VERBOSE)
  ENDIF ELSE L1B_DATA = GET_PARASOL_L1B_DATA(FILENAME,ENDIAN_SIZE=ENDIAN_SIZE) 
  
  DD = L1B_DATA[*].COLUMN
  IF N_ELEMENTS(DD) LT 5 THEN RETURN,-1
    
  TEMP_SZA = COS(L1B_DATA.SZA[5]*!DTOR)

  IF KEYWORD_SET(RGB) THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: RGB JPEG SELECTED'

    QL_DATAR = L1B_DATA.REF_865P[5]/TEMP_SZA
    QL_DATAG = L1B_DATA.REF_565NP[5]/TEMP_SZA
    QL_DATAB = L1B_DATA.REF_443NP[5]/TEMP_SZA
    
  ENDIF ELSE BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: RETRIEVING RADIANCE 13 DATA'
    QL_DATAR  = L1B_DATA.REF_865P[5]/TEMP_SZA
  ENDELSE
  
  IF QL_DATAr[0] EQ -1 THEN BEGIN
    PRINT,'PARASOL L1B QUICKLOOK: ERROR, PROBLEM ENCOUNTERED DURING L1B RADIANCE RETRIEVAL, CHECK PRODUCTS IS A PARASOL L1B PRODUCT'
    RETURN,-1
  ENDIF

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: GET PRODUCT DIMENSIONS AND DEFINE IMAGE ARRAY'
  QL_DIMS  = SIZE(QL_DATAR)

;-----------------------------------------------  
; REGRID DATA A LARGER SIZE  

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: REGRIDDING THE QUICKLOOK INTO A LARGER ARRAY'
  MFACT = 5.0
  QL_DATAR = CONGRID(QL_DATAR,MFACT*QL_DIMS[1],MFACT*QL_DIMS[2])
  IF KEYWORD_SET(RGB) THEN BEGIN 
  QL_DATAG = CONGRID(QL_DATAG,MFACT*QL_DIMS[1],MFACT*QL_DIMS[2]) 
  QL_DATAB = CONGRID(QL_DATAB,MFACT*QL_DIMS[1],MFACT*QL_DIMS[2]) 
  ENDIF
  
  QL_DIMS_NEW  = SIZE(QL_DATAR)
  QL_IMAGE = BYTARR(3,QL_DIMS_NEW[1],QL_DIMS_NEW[2])
  
;-----------------------------------------------
; POPULATE THE QUICKLOOK IMAGE ARRAY  

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: ADD RADIANCE DATA TO IMAGE ARRAY CHANNELS'
  TOP_RAD = 200.0
  IF KEYWORD_SET(RGB) THEN BEGIN 
   
    TEMP_MAX = MAX([MAX(QL_DATAR),MAX(QL_DATAG),MAX(QL_DATAB)])
    QL_IMAGE[0,*,*]=BYTSCL(QL_DATAR,MAX=TEMP_MAX,TOP=TOP_RAD)
    QL_IMAGE[1,*,*]=BYTSCL(QL_DATAG,MAX=TEMP_MAX,TOP=TOP_RAD)
    QL_IMAGE[2,*,*]=BYTSCL(QL_DATAB,MAX=TEMP_MAX,TOP=TOP_RAD)

;----------------------------------------------
; RELEASE MEMORY FOR GREEN AND BLUE CHANNELS
    
    QL_DATAG = 0
    QL_DATAB = 0
    
  ENDIF ELSE BEGIN
    QL_IMAGE[0,*,*]=BYTSCL(QL_DATAR,TOP=TOP_RAD)
    QL_IMAGE[1,*,*]=BYTSCL(QL_DATAR,TOP=TOP_RAD)
    QL_IMAGE[2,*,*]=BYTSCL(QL_DATAR,TOP=TOP_RAD)
  ENDELSE

;---------------------------------------------------
; GET LAT/LON DATA FOR ROI PIXEL INDEX IF REQUESTED

  IF KEYWORD_SET(ROI) THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: OVERLAY OF ROI SELECTED'
    IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: RETRIEVING PRODUCT GEOLOCATION'
     
;---------------------------------------------------
; REVERSE GEOLOCATION ARRAYS INTO SAME ORIENTAITON AS RADIANCE

    QL_GEO_LAT = CONGRID(L1B_DATA.LATITUDE,MFACT*QL_DIMS[1],MFACT*QL_DIMS[2])
    QL_GEO_LON = CONGRID(L1B_DATA.LONGITUDE,MFACT*QL_DIMS[1],MFACT*QL_DIMS[2])
           
    QL_ROI_IDX = WHERE($
          QL_GEO_LAT LT ICOORDS[0] AND $
          QL_GEO_LAT GT ICOORDS[1] AND $
          QL_GEO_LON LT ICOORDS[2] AND $
          QL_GEO_LON GT ICOORDS[3] )

;---------------------------------------------------
; CONVERT ROI PIXELS TO A LIGHT RED OVERLAY

    IF QL_ROI_IDX[0] GT -1 THEN BEGIN
          IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: MODIFYING RED CHANNEL FOR ROI OVERLAY'
          ;QL_DATAR = BYTSCL(QL_DATAR,TOP=TOP_RAD)
          ;QL_DATAR[QL_ROI_IDX]=255
          ;QL_IMAGE[0,*,*] = QL_DATAR
            QL_DATAR = QL_IMAGE[0,*,*]
            QL_DATAG = QL_IMAGE[1,*,*]
            QL_DATAB = QL_IMAGE[2,*,*]
          
            QL_DATAR[QL_ROI_IDX]=250
            QL_DATAG[QL_ROI_IDX]=bytscl(QL_DATAG[QL_ROI_IDX],top=75)
            QL_DATAB[QL_ROI_IDX]=bytscl(QL_DATAb[QL_ROI_IDX],top=75)
            QL_IMAGE[0,*,*] = QL_DATAR
            QL_IMAGE[1,*,*] = QL_DATAG
            QL_IMAGE[2,*,*] = QL_DATAB
          
    ENDIF
  ENDIF

;---------------------------------------------------
; OUTPUT QUICKLOOK AS JPEG

  IF KEYWORD_SET(VERBOSE) THEN PRINT, 'PARASOL L1B QUICKLOOK: WRITING IMAGE TO JPEG'
  WRITE_JPEG ,QL_PARASOL_JPG,QL_IMAGE,TRUE=1,/ORDER
  
;---------------------------------------------------
; RETURN A POSITIVE VALUE INDICATING QL GENERATION OK

  RETURN, 1

END
