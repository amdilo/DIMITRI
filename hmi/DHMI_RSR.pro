;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      DHMI_RSR    
;* 
;* PURPOSE:
;*      THIS PROGRAM DISPLAYS A WIDGET ALLOWING SPECIFICATION OF THE REQUIRED PARAMETERS 
;*      TO START THE OBJET GRAPHICS INTERFACE. USER'S ARE REQUESTED TO PROVIDE THE START 
;*      AND STOP WAVELENGTH OF INTEREST. USERS CAN EITHER TYPE SPECFIC WAVELENGTHS, OR 
;*      USE THE BUTTONS TO MODIFY THE WAVELENGTHS. 
;*
;* CALLING SEQUENCE:
;*      DHMI_RSR      
;*
;* INPUTS:
;*      NONE
;*
;* KEYWORDS:
;*      GROUP_LEADER - THE ID OF ANOTHER WIDGET TO BE USED AS THE GROUP LEADER
;*      VERBOSE      - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      NONE
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      28 FEB 2011 - C KENT    - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      14 APR 2011 - C KENT    - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                                COMPILATION AND OPERATION      
;*
;**************************************************************************************
;**************************************************************************************

PRO DHMI_RSR_START,EVENT

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_RSR_INFO, /NO_COPY
  WIDGET_CONTROL, EVENT.ID,   GET_UVALUE=ACTION

  IF N_ELEMENTS(ACTION) EQ 0 THEN ACTION = ''

;--------------------------
; GET START AND STOP WIDGETS

  DHMI_RSR_INFO.START_WL->GETPROPERTY,VALUE=START_NM
  DHMI_RSR_INFO.STOP_WL->GETPROPERTY,VALUE=STOP_NM

;--------------------------
; GET SCREEN DIMENSIONS FOR 
; CENTERING WIDGET

  DIMS  = GET_SCREEN_SIZE()
  XSIZE = 200
  YSIZE = 60
  XLOC  = (DIMS[0]/2)-(XSIZE/2)
  YLOC  = (DIMS[1]/2)-(YSIZE/2)

;--------------------------
; DESTROY THE INTERFACE WIDGET

  OBJ_DESTROY,DHMI_RSR_INFO.START_WL
  OBJ_DESTROY,DHMI_RSR_INFO.STOP_WL
  WIDGET_CONTROL,EVENT.TOP,/DESTROY

;--------------------------
; IF START BUTTON PRESSED THEN 
; CREATE A POP WIDGET AND START 
; THE RSR INTERFACE

  IF ACTION EQ 'START' THEN BEGIN
    INFO_WD = WIDGET_BASE(COLUMN=1, XSIZE=XSIZE, YSIZE=YSIZE, TITLE='Please Wait...',XOFFSET=XLOC,YOFFSET=YLOC)
    LBLTXT  = WIDGET_LABEL(INFO_WD,VALUE=' ')
    LBLTXT  = WIDGET_LABEL(INFO_WD,VALUE='Please wait,')
    LBLTXT  = WIDGET_LABEL(INFO_WD,VALUE='Loading in progress...')
    WIDGET_CONTROL, INFO_WD, /REALIZE
    WIDGET_CONTROL, /HOURGLASS
    IF DHMI_RSR_INFO.GLEADER GT -1 THEN PLOT_DIMITRI_SENSOR_RSR,[START_NM,STOP_NM],GROUP_LEADER=DHMI_RSR_INFO.GLEADER,VERBOSE=DHMI_RSR_INFO.IVERBOSE $ 
    	ELSE PLOT_DIMITRI_SENSOR_RSR,[START_NM,STOP_NM],VERBOSE=DHMI_RSR_INFO.IVERBOSE
    WIDGET_CONTROL,INFO_WD,/DESTROY
  ENDIF

END

PRO DHMI_RSR_CHANGE,EVENT

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_RSR_INFO, /NO_COPY
  WIDGET_CONTROL, EVENT.ID,   GET_UVALUE=ACTION


  DHMI_RSR_INFO.START_WL->GETPROPERTY,VALUE=START_NM
  DHMI_RSR_INFO.STOP_WL->GETPROPERTY, VALUE=STOP_NM

  UP_VAL = 10

  CASE ACTION OF
    'START<':START_NM=START_NM-UP_VAL
    'START>':START_NM=START_NM+UP_VAL
    'STOP<' :STOP_NM=STOP_NM-UP_VAL
    'STOP>' :STOP_NM=STOP_NM+UP_VAL
  ENDCASE

  IF START_NM LT 0.0 THEN START_NM  = 0
  IF STOP_NM  LT 0.0 THEN STOP_NM   = 0

  DHMI_RSR_INFO.START_WL->SETPROPERTY,VALUE=START_NM
  DHMI_RSR_INFO.STOP_WL->SETPROPERTY,VALUE=STOP_NM
  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_RSR_INFO, /NO_COPY

END

PRO DHMI_RSR,GROUP_LEADER=GROUP_LEADER,VERBOSE=VERBOSE

;------------------------------------ 
; GET THE DISPLAY RESOLUTION FOR WIDGET POSITIONING

  IF KEYWORD_SET(VERBOSE) THEN BEGIN
  PRINT,'DHMI_INGEST: RETRIEVEING SCREEN DIMENSIONS FOR WIDGET' 
  IVERBOSE=1
  ENDIF ELSE IVERBOSE=0
  IF STRUPCASE(!VERSION.OS_FAMILY) EQ 'WINDOWS' THEN WIN_FLAG = 1 ELSE WIN_FLAG = 0
   
  DIMS  = GET_SCREEN_SIZE()
  IF WIN_FLAG THEN XSIZE = 310 ELSE XSIZE = 350
  YSIZE = 200
  OPT_BTN=60
  XLOC  = (DIMS[0]/2)-(XSIZE/2)
  YLOC  = (DIMS[1]/2)-(YSIZE/2)

  DHMI_RSR_TLB      = WIDGET_BASE(TITLE='DIMITRI V2.0: RSR SETUP',COLUMN=1,XSIZE=XSIZE,XOFFSET=XLOC,YOFFSET=YLOC)
  DHMI_RSR_TLB_LBL  = WIDGET_LABEL(DHMI_RSR_TLB,VALUE='')

  DHMI_RSR_TLB_WL           = WIDGET_BASE(DHMI_RSR_TLB,ROW=2,FRAME=1)
  DHMI_RSR_TLB_WL_STARTID   = FSC_FIELD(DHMI_RSR_TLB_WL, TITLE='Start Wavelength (nm) : ', VALUE=400, OBJECT=START_WL)
  DHMI_RSR_TLB_WL_TMP       = WIDGET_BUTTON(DHMI_RSR_TLB_WL,VALUE='<',UVALUE= 'START<', EVENT_PRO='DHMI_RSR_CHANGE')
  DHMI_RSR_TLB_WL_TMP       = WIDGET_BUTTON(DHMI_RSR_TLB_WL,VALUE='>',UVALUE= 'START>', EVENT_PRO='DHMI_RSR_CHANGE') 
  IF WIN_FLAG THEN DHMI_RSR_TLB_WL_STOPID    = FSC_FIELD(DHMI_RSR_TLB_WL, TITLE='Stop Wavelength (nm) : ', VALUE=1000, OBJECT=STOP_WL) $
    ELSE DHMI_RSR_TLB_WL_STOPID    = FSC_FIELD(DHMI_RSR_TLB_WL, TITLE='Stop Wavelength (nm)  : ', VALUE=1000, OBJECT=STOP_WL)
  DHMI_RSR_TLB_WL_TMP       = WIDGET_BUTTON(DHMI_RSR_TLB_WL,VALUE='<',UVALUE= 'STOP<',  EVENT_PRO='DHMI_RSR_CHANGE')
  DHMI_RSR_TLB_WL_TMP       = WIDGET_BUTTON(DHMI_RSR_TLB_WL,VALUE='>',UVALUE= 'STOP>',  EVENT_PRO='DHMI_RSR_CHANGE') 

  DHMI_RSR_TLB_LBL  = WIDGET_LABEL(DHMI_RSR_TLB,VALUE='')

  DHMI_RSR_TLB_BTN        = WIDGET_BASE(DHMI_RSR_TLB,ROW=1,/ALIGN_RIGHT)
  DHMI_RSR_TLB_BTN_TMP    = WIDGET_BUTTON(DHMI_RSR_TLB_BTN,VALUE='Start',UVALUE= 'START',EVENT_PRO='DHMI_RSR_START',XSIZE=OPT_BTN) 
  DHMI_RSR_TLB_BTN_TMP    = WIDGET_BUTTON(DHMI_RSR_TLB_BTN,VALUE='Close',EVENT_PRO='DHMI_RSR_START',XSIZE=OPT_BTN)

  IF KEYWORD_SET(GROUP_LEADER) THEN GLEADER=GROUP_LEADER ELSE GLEADER=-1
  INFO = {$
          IVERBOSE  : IVERBOSE  ,$
          START_WL  : START_WL ,$
          STOP_WL   : STOP_WL  ,$
          GLEADER   : GLEADER  $
          }

  IF KEYWORD_SET(GROUP_LEADER) THEN WIDGET_CONTROL, DHMI_RSR_TLB, /REALIZE,SET_UVALUE=INFO,/NO_COPY,GROUP_LEADER=GROUP_LEADER $
    ELSE WIDGET_CONTROL, DHMI_RSR_TLB, /REALIZE,SET_UVALUE=INFO,/NO_COPY
  XMANAGER,'DHMI_RSR', DHMI_RSR_TLB

END