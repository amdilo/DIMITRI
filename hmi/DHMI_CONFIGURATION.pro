;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      DHMI_CONFIGURATION 
;* 
;* PURPOSE:
;*      THIS PRGRAM GENERATES AN INTERACTIVE WIDGET ALLOWING CHANGES TO BE MADE TO 
;*      THE DEFAULT DIMITRI CONFIGURAITON PARAMETERS.
;* 
;* CALLING SEQUENCE:
;*      DHMI_CONFIGURATION      
;* 
;* INPUTS:
;*      NONE
;*
;* KEYWORDS:
;*      GROUP_LEADER  - ID OF THE WIDGET TO BE USED AS THE GROUP LEADER
;*      VERBOSE       - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      NONE 
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      22 MAR 2011 - C KENT    - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      14 APR 2011 - C KENT    - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                                COMPILATION AND OPERATION        
;*
;**************************************************************************************
;**************************************************************************************

PRO DHMI_CONFIGURATION_QL,EVENT

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP, GET_UVALUE=DHMI_CFIG_INFO, /NO_COPY

;---------------------
; UPDATE CURRENT_BUTTON WITH SELECTION

  IF DHMI_CFIG_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CONFIGURATION->QL: UPDATING CURRENT BUTTON SELECTION'
  DHMI_CFIG_INFO.CURRENT_BUTTON = EVENT.ID
  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_CFIG_INFO, /NO_COPY

END

;**************************************************************************************
;**************************************************************************************

PRO DHMI_CONFIGURATION_SAVE,EVENT

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_CFIG_INFO, /NO_COPY

;----------- ---------------
; RETRIEVE FSC_FIELD VALUES

  IF DHMI_CFIG_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CONFIGURATION->SAVE: RETRIEVEING FSC_FILED VALUES'
  DHMI_CFIG_INFO.FSC_DO_XSIZE            ->GETPROPERTY, VALUE=XSIZE
  DHMI_CFIG_INFO.FSC_DO_YSIZE            ->GETPROPERTY, VALUE=YSIZE
  DHMI_CFIG_INFO.FSC_DO_CTAB             ->GETPROPERTY, VALUE=CTAB
  DHMI_CFIG_INFO.FSC_P1_VZA              ->GETPROPERTY, VALUE=VZA
  DHMI_CFIG_INFO.FSC_P1_CLOUD            ->GETPROPERTY, VALUE=P1_CLOUD
  DHMI_CFIG_INFO.FSC_P1_SZA              ->GETPROPERTY, VALUE=SZA
  DHMI_CFIG_INFO.FSC_P1_ROI              ->GETPROPERTY, VALUE=P1_ROI
  DHMI_CFIG_INFO.FSC_P1_RAA              ->GETPROPERTY, VALUE=RAA
  DHMI_CFIG_INFO.FSC_P1_DAY              ->GETPROPERTY, VALUE=P1_DAY
  DHMI_CFIG_INFO.FSC_P2_BIN              ->GETPROPERTY, VALUE=BBP
  DHMI_CFIG_INFO.FSC_P2_CLOUD            ->GETPROPERTY, VALUE=P2_CLOUD
  DHMI_CFIG_INFO.FSC_P2_OBS              ->GETPROPERTY, VALUE=BON
  DHMI_CFIG_INFO.FSC_P2_ROI              ->GETPROPERTY, VALUE=P2_ROI
  IF DHMI_CFIG_INFO.CURRENT_BUTTON EQ DHMI_CFIG_INFO.DHMI_CFIG_TLB_DO_B_RGB1 THEN RGB = 1.0 ELSE RGB= 0.0

;--------------------------
; UPDATE CONFIGURATION DATA

  IF DHMI_CFIG_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CONFIGURATION->SAVE: UPDATING CONFIGURATION INFORMATION'
  DHMI_CFIG_INFO.CFIG_DATA.(1)[0] = XSIZE 
  DHMI_CFIG_INFO.CFIG_DATA.(1)[1] = YSIZE
  DHMI_CFIG_INFO.CFIG_DATA.(1)[2] = CTAB
  DHMI_CFIG_INFO.CFIG_DATA.(1)[3] = RGB
  DHMI_CFIG_INFO.CFIG_DATA.(1)[4] = VZA
  DHMI_CFIG_INFO.CFIG_DATA.(1)[5] = SZA
  DHMI_CFIG_INFO.CFIG_DATA.(1)[6] = RAA
  DHMI_CFIG_INFO.CFIG_DATA.(1)[7] = P1_CLOUD
  DHMI_CFIG_INFO.CFIG_DATA.(1)[8] = P1_ROI
  DHMI_CFIG_INFO.CFIG_DATA.(1)[9] = P1_DAY
  DHMI_CFIG_INFO.CFIG_DATA.(1)[10] = BBP
  DHMI_CFIG_INFO.CFIG_DATA.(1)[11] = BON
  DHMI_CFIG_INFO.CFIG_DATA.(1)[12] = P2_CLOUD
  DHMI_CFIG_INFO.CFIG_DATA.(1)[13] = P2_ROI

;--------------------------
; SAVE NEW CONFIGURATION INFORMATION

  IF DHMI_CFIG_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CONFIGURATION->SAVE: SAVING NEW CONFIGURAITON INFORMATION'
  RES = SAVE_DIMITRI_CONFIGURATION(DHMI_CFIG_INFO.CFIG_DATA)
  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_CFIG_INFO, /NO_COPY
  MSG = ['Configuration file updated!']
  IF RES EQ 1 THEN TEMP = DIALOG_MESSAGE(MSG,/INFORMATION,/CENTER)

END

;**************************************************************************************
;**************************************************************************************

PRO DHMI_CONFIGURATION_EXIT,EVENT

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_CFIG_INFO, /NO_COPY

;--------------------------
; CLEAN UP OBJECTS

  IF DHMI_CFIG_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CONFIGURATION->EXIT: DESTROYING FSC OBJECTS'
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_DO_XSIZE
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_DO_YSIZE
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_DO_CTAB
    
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P1_CLOUD
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P1_ROI
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P1_DAY
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P1_VZA
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P1_SZA
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P1_RAA

  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P2_BIN
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P2_BIN
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P2_CLOUD
  OBJ_DESTROY,DHMI_CFIG_INFO.FSC_P2_OBS

  IF DHMI_CFIG_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CONFIGURATION->EXIT: DESTROYING WIDGET'
  WIDGET_CONTROL,EVENT.TOP,/DESTROY

END

;**************************************************************************************
;**************************************************************************************

PRO DHMI_CONFIGURATION,GROUP_LEADER=GROUP_LEADER,VERBOSE=VERBOSE

;--------------------------
; GET CURRENT CONFIGURATION VALUES
 
  CFIG_DATA = GET_DIMITRI_CONFIGURATION()
  IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT, 'DHMI_CONFIGURATION: STARTING CONFIGURATION WIDGET SETUP'
    IVERBOSE = 1 
  ENDIF ELSE IVERBOSE = 0
  IF STRUPCASE(!VERSION.OS_FAMILY) EQ 'WINDOWS' THEN WIN_FLAG = 1 ELSE WIN_FLAG = 0

;--------------------------
; DEFINE WIDGET PARAMETERS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CONFIGURATION: RETRIEVING SCREEN DIMENSIONS FOR WIDGET'
  DIMS  = GET_SCREEN_SIZE()
  XSIZE = 525
  
  IF WIN_FLAG THEN YSIZE = 305 ELSE BEGIN
    XSIZE = 600
    YSIZE = 370
  ENDELSE
  
  OPT_BTN=60
  XLOC  = (DIMS[0]/2)-(XSIZE/2)
  YLOC  = (DIMS[1]/2)-(YSIZE/2)

;--------------------------
; DEFINE WIDGET

  FSC_SMALL = 10
  FSC_LARGE = 8

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CONFIGURATION: DEFINING CONFIGURATION WIDGET'
  DHMI_CFIG_TLB = WIDGET_BASE(COLUMN=1,TITLE='DIMITRI V2.0: CONFIGURATION',XSIZE=XSIZE,$
                                  XOFFSET=XLOC,YOFFSET=YLOC,YSIZE=YSIZE)

  
  DHMI_CFIG_TLB_DO = WIDGET_BASE(DHMI_CFIG_TLB,FRAME=1,COLUMN=1)
  DHMI_CFIG_TLB_DO_LBL = WIDGET_LABEL(DHMI_CFIG_TLB_DO,VALUE='DIMITRI OPTIONS :',/ALIGN_LEFT)
    
  ; DIMITRI OPTIONS
  DHMI_CFIG_TLB_DO_A = WIDGET_BASE(DHMI_CFIG_TLB_DO,ROW=1,/GRID_LAYOUT)
  
  IF WIN_FLAG THEN BEGIN  
    DHMI_CFIG_TLB_DO_A_XSIZEID = FSC_FIELD(DHMI_CFIG_TLB_DO_A,TITLE='PLOT X-SIZE : ',     VALUE=FIX(CFIG_DATA.(1)[0]),OBJECT=FSC_DO_XSIZE,XSIZE=FSC_SMALL,DECIMAL=0)
    DHMI_CFIG_TLB_DO_A_YSIZEID = FSC_FIELD(DHMI_CFIG_TLB_DO_A,TITLE='PLOT Y-SIZE : ',     VALUE=FIX(CFIG_DATA.(1)[1]),OBJECT=FSC_DO_YSIZE,XSIZE=FSC_SMALL,DECIMAL=0)  
    DHMI_CFIG_TLB_DO_A_CTABID  = FSC_FIELD(DHMI_CFIG_TLB_DO_A,TITLE='COLOUR TAB : ',VALUE=FIX(CFIG_DATA.(1)[2]),OBJECT=FSC_DO_CTAB,XSIZE=FSC_SMALL,DECIMAL=0)  
  ENDIF ELSE BEGIN
    DHMI_CFIG_TLB_DO_A_XSIZEID = FSC_FIELD(DHMI_CFIG_TLB_DO_A,TITLE='PLOT X-SIZE : ',     VALUE=FIX(CFIG_DATA.(1)[0]),OBJECT=FSC_DO_XSIZE,XSIZE=FSC_SMALL,DECIMAL=0)
    DHMI_CFIG_TLB_DO_A_YSIZEID = FSC_FIELD(DHMI_CFIG_TLB_DO_A,TITLE='PLOT Y-SIZE : ',     VALUE=FIX(CFIG_DATA.(1)[1]),OBJECT=FSC_DO_YSIZE,XSIZE=FSC_SMALL,DECIMAL=0)  
    DHMI_CFIG_TLB_DO_A_CTABID  = FSC_FIELD(DHMI_CFIG_TLB_DO_A,TITLE='COLOUR TAB  : ',VALUE=FIX(CFIG_DATA.(1)[2]),OBJECT=FSC_DO_CTAB,XSIZE=FSC_SMALL,DECIMAL=0)  
  ENDELSE

  DHMI_CFIG_TLB_DO_B         = WIDGET_BASE(DHMI_CFIG_TLB_DO,ROW=1)
  DHMI_CFIG_TLB_DO_B_LBL     = WIDGET_LABEL(DHMI_CFIG_TLB_DO_B,VALUE=' QUICKLOOKS :')
  DHMI_CFIG_TLB_DO_B_RGB     = WIDGET_BASE(DHMI_CFIG_TLB_DO_B,ROW=1,/EXCLUSIVE)  
  DHMI_CFIG_TLB_DO_B_RGB1    = WIDGET_BUTTON(DHMI_CFIG_TLB_DO_B_RGB,VALUE='RGB',EVENT_PRO='DHMI_CONFIGURATION_QL')
  DHMI_CFIG_TLB_DO_B_RGB2    = WIDGET_BUTTON(DHMI_CFIG_TLB_DO_B_RGB,VALUE='GREYSCALE',EVENT_PRO='DHMI_CONFIGURATION_QL')    
 
  IF FIX(CFIG_DATA.(1)[3]) EQ 1 THEN BEGIN
    WIDGET_CONTROL, DHMI_CFIG_TLB_DO_B_RGB1, SET_BUTTON=1 
    CURRENT_BUTTON = DHMI_CFIG_TLB_DO_B_RGB1
  ENDIF ELSE BEGIN
    WIDGET_CONTROL, DHMI_CFIG_TLB_DO_B_RGB2, SET_BUTTON=1  
    CURRENT_BUTTON = DHMI_CFIG_TLB_DO_B_RGB2
  ENDELSE
  
  ;PROCESS 1 OPTIONS

  DHMI_CFIG_TLB_P1     = WIDGET_BASE(DHMI_CFIG_TLB,FRAME=1,COLUMN=1)
  DHMI_CFIG_TLB_P1_LBL = WIDGET_LABEL(DHMI_CFIG_TLB_P1,VALUE='SENSOR RECALIBRATION OPTIONS :',/ALIGN_LEFT)
  
  DHMI_CFIG_TLB_P1_A = WIDGET_BASE(DHMI_CFIG_TLB_P1,COLUMN=3,/GRID_LAYOUT)
  
  IF WIN_FLAG THEN BEGIN  
    DHMI_CFIG_TLB_P1_A_VZAID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CHI_VZA    : ',   VALUE=CFIG_DATA.(1)[4],OBJECT=FSC_P1_VZA,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_CLOUDID = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CLOUD %   : ',   VALUE=CFIG_DATA.(1)[7],OBJECT=FSC_P1_CLOUD,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_SZAID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CHI_SZA   : ',   VALUE=CFIG_DATA.(1)[5],OBJECT=FSC_P1_SZA,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_ROIID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='ROI %        : ',   VALUE=CFIG_DATA.(1)[8],OBJECT=FSC_P1_ROI,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_RAAID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CHI_RAA        : ',   VALUE=CFIG_DATA.(1)[6],OBJECT=FSC_P1_RAA,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_DAYID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='DAY OFFSET : ',   VALUE=FIX(CFIG_DATA.(1)[9]),OBJECT=FSC_P1_DAY,XSIZE=FSC_SMALL,DECIMAL=0)
  ENDIF ELSE BEGIN
    DHMI_CFIG_TLB_P1_A_VZAID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CHI_VZA    : ',   VALUE=CFIG_DATA.(1)[4],OBJECT=FSC_P1_VZA,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_CLOUDID = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CLOUD %    : ',   VALUE=CFIG_DATA.(1)[7],OBJECT=FSC_P1_CLOUD,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_SZAID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CHI_SZA    : ',   VALUE=CFIG_DATA.(1)[5],OBJECT=FSC_P1_SZA,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_ROIID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='ROI %      : ',   VALUE=CFIG_DATA.(1)[8],OBJECT=FSC_P1_ROI,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_RAAID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='CHI_RAA    : ',   VALUE=CFIG_DATA.(1)[6],OBJECT=FSC_P1_RAA,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P1_A_DAYID   = FSC_FIELD(DHMI_CFIG_TLB_P1_A,TITLE='DAY OFFSET : ',   VALUE=FIX(CFIG_DATA.(1)[9]),OBJECT=FSC_P1_DAY,XSIZE=FSC_SMALL,DECIMAL=0)
  ENDELSE
  
  
  ;PROCESS 2 OPTIONS

  DHMI_CFIG_TLB_P2     = WIDGET_BASE(DHMI_CFIG_TLB,FRAME=1,COLUMN=1)
  DHMI_CFIG_TLB_P2_LBL = WIDGET_LABEL(DHMI_CFIG_TLB_P2,VALUE='VGT SIMULATION OPTIONS :',/ALIGN_LEFT)
  
  DHMI_CFIG_TLB_P2_A = WIDGET_BASE(DHMI_CFIG_TLB_P2,COLUMN=2,/GRID_LAYOUT)
  
  IF WIN_FLAG THEN BEGIN
    DHMI_CFIG_TLB_P2_A_BINID   = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='BIN PERIOD   : ',   VALUE=FIX(CFIG_DATA.(1)[10]),OBJECT=FSC_P2_BIN,XSIZE=FSC_SMALL,DECIMAL=0)
    DHMI_CFIG_TLB_P2_A_CLOUDID = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='VGT CLOUD %  : ',   VALUE=CFIG_DATA.(1)[12],OBJECT=FSC_P2_CLOUD,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P2_A_OBSID   = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='MIN NUM OBS. : ',   VALUE=FIX(CFIG_DATA.(1)[11]),OBJECT=FSC_P2_OBS,XSIZE=FSC_SMALL,DECIMAL=0)
    DHMI_CFIG_TLB_P2_A_ROIID   = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='VGT ROI %    : ',   VALUE=CFIG_DATA.(1)[13],OBJECT=FSC_P2_ROI,XSIZE=FSC_SMALL,DECIMAL=2)
  ENDIF ELSE BEGIN
    DHMI_CFIG_TLB_P2_A_BINID   = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='BIN PERIOD   : ',   VALUE=FIX(CFIG_DATA.(1)[10]),OBJECT=FSC_P2_BIN,XSIZE=FSC_SMALL,DECIMAL=0)
    DHMI_CFIG_TLB_P2_A_CLOUDID = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='VGT CLOUD %  : ',   VALUE=CFIG_DATA.(1)[12],OBJECT=FSC_P2_CLOUD,XSIZE=FSC_SMALL,DECIMAL=2)
    DHMI_CFIG_TLB_P2_A_OBSID   = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='MIN NUM OBS. : ',   VALUE=FIX(CFIG_DATA.(1)[11]),OBJECT=FSC_P2_OBS,XSIZE=FSC_SMALL,DECIMAL=0)
    DHMI_CFIG_TLB_P2_A_ROIID   = FSC_FIELD(DHMI_CFIG_TLB_P2_A,TITLE='VGT ROI %    : ',   VALUE=CFIG_DATA.(1)[13],OBJECT=FSC_P2_ROI,XSIZE=FSC_SMALL,DECIMAL=2)  
  ENDELSE
 
  ; SAVE AND EXIT BUTTONS

  DHMI_CFIG_TLB_BOT      = WIDGET_BASE(DHMI_CFIG_TLB,ROW=1,/ALIGN_RIGHT)
  DHMI_CFIG_TLB_BOT_SAVE = WIDGET_BUTTON(DHMI_CFIG_TLB_BOT,VALUE='Save',UVALUE='SAVE',EVENT_PRO='DHMI_CONFIGURATION_SAVE',XSIZE=OPT_BTN)
  DHMI_CFIG_TLB_BOT_EXIT = WIDGET_BUTTON(DHMI_CFIG_TLB_BOT,VALUE='Exit',UVALUE='EXIT',EVENT_PRO='DHMI_CONFIGURATION_EXIT',XSIZE=OPT_BTN)

;--------------------------
; STORE WIDGET INFO IN A STRUCTURE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CONFIGURATION: STORING WIDGET INFORMATION'
  IF NOT KEYWORD_SET(GROUP_LEADER) THEN GROUP_LEADER = DHMI_CFIG_TLB
  DHMI_CFIG_INFO = {                                        $
                    CURRENT_BUTTON            : CURRENT_BUTTON      ,$
                    IVERBOSE                  : IVERBOSE            ,$
                    CFIG_DATA                 : CFIG_DATA           ,$
                    FSC_DO_XSIZE              : FSC_DO_XSIZE           ,$
                    FSC_DO_YSIZE              : FSC_DO_YSIZE           ,$
                    FSC_DO_CTAB               : FSC_DO_CTAB         ,$
                    FSC_P1_VZA                : FSC_P1_VZA          ,$
                    FSC_P1_CLOUD              : FSC_P1_CLOUD        ,$
                    FSC_P1_SZA                : FSC_P1_SZA          ,$
                    FSC_P1_ROI                : FSC_P1_ROI          ,$
                    FSC_P1_RAA                : FSC_P1_RAA          ,$
                    FSC_P1_DAY                : FSC_P1_DAY          ,$
                    FSC_P2_BIN                : FSC_P2_BIN          ,$
                    FSC_P2_CLOUD              : FSC_P2_CLOUD        ,$
                    FSC_P2_OBS                : FSC_P2_OBS          ,$
                    FSC_P2_ROI                : FSC_P2_ROI          ,$
                    DHMI_CFIG_TLB_DO_B_RGB1   : DHMI_CFIG_TLB_DO_B_RGB1  ,$
                    DHMI_CFIG_TLB_DO_B_RGB2   : DHMI_CFIG_TLB_DO_B_RGB2   $
                   }

;--------------------------
; REALISE THE WIDGET AND REGISTER 
; WITH THE XMANAGER

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CONFIGURATION: REALISING THE WIDGET'
  WIDGET_CONTROL, DHMI_CFIG_TLB,/REALIZE,SET_UVALUE=DHMI_CFIG_INFO,GROUP_LEADER=GROUP_LEADER
  XMANAGER,'DHMI_CONFIGURATION',DHMI_CFIG_TLB

END