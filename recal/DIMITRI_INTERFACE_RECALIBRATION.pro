;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      DIMITRI_INTERFACE_RECALIBRATION       
;* 
;* PURPOSE:
;*      INTERFACES BETWEEN THE DIMITRI HMI AND THE RECALIBRATE_DOUBLETS FUNCTION
;* 
;* CALLING SEQUENCE:
;*      RES = DIMITRI_INTERFACE_RECALIBRATION(OUTPUT_FOLDER,IC_REGION,REF_SENSORS,REF_PROC_VERS, $
;*                                            CAL_SENSORS,CAL_PROC_VERS)      
;* 
;* INPUTS:
;*      OUTPUT_FOLDER     - THE FULL PATH OF THE OUTPUT FOLDER  
;*      IC_REGION         - THE VALIDATION SITE NAME E.G. 'Uyuni'
;*      REF_SENSORS       - A STRING ARRAY CONTAINING THE SENSOR NAMES TO BE TREATED AS 
;*                          REFERENCE SENSORS DURING INTERCALIBRATION
;*      REF_PROC_VERS     - A STRING ARRAY OF THE REFERENCE SENSOR PROCESSING VERSIONS
;*      CAL_SENSORS       - A STRING ARRAY CONTAINING THE SENSOR NAMES TO BE 
;*                          INTERCALIBRATED AGAINST THE REFERENCE SENSORS
;*      CAL_PROC_VERS     - A STRING ARRAY OF THE CALIBRATION SENSOR PROCESSING VERSIONS
;*      VZA_MIN           - THE MINIMUM VIEWING ZENITH ANGLE ALLOWED FOR AN OBSERVATION
;*      VZA_MAX           - THE MAXIMUM VIEWING ZENITH ANGLE ALLOWED FOR AN OBSERVATION
;*      VAA_MIN           - THE MINIMUM VIEWING AZIMUTH ANGLE ALLOWED FOR AN OBSERVATION
;*      VAA_MAX           - THE MAXIMUM VIEWING AZIMUTH ANGLE ALLOWED FOR AN OBSERVATION
;*      SZA_MIN           - THE MINIMUM SOLAR ZENITH ANGLE ALLOWED FOR AN OBSERVATION
;*      SZA_MAX           - THE MAXIMUM SOLAR ZENITH ANGLE ALLOWED FOR AN OBSERVATION
;*      SAA_MIN           - THE MINIMUM SOLAR AZIMUTH ANGLE ALLOWED FOR AN OBSERVATION
;*      SAA_MAX           - THE MAXIMUM SOLAR AZIMTUH ANGLE ALLOWED FOR AN OBSERVATION
;
;*
;* KEYWORDS:
;*      ALL               - SET TO RUN INTERCALIBRATION ON ALL COMBINATIONS OF REF-CAL 
;*                          SENSOR PROVIDED. IF NOT SET THEN REF_SENSORS AND CAL_SENSORS 
;*                          MUST BE THE SAME DIMENSIONS
;*      VERBOSE           - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      STATUS            - 1: NO ERRORS REPORTED, (-1) OR 0: ERRORS DURING INGESTION 
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*                  - M BOUVET  - PROTOTYPE DIMITRI VERSION
;*      22 JAN 2011 - C KENT    - DIMITRI-2 V1.0
;*      02 JUL 2011 - C KENT    - ADDED ABSOLUTE ANGULAR CRITERIA
;*
;* VALIDATION HISTORY:
;*      13 APR 2011 - C KENT    - WINDOWS 32-BIT MACHINE IDL 8.0 AND LINUX 64-BIT MACHINE 
;*                                  IDL 8.0, NOMINAL COMPILATION AND OPERATION. TESTED FOR 
;*                                  MERIS VS MERIS AND MERIS VS MODIS 
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION DIMITRI_INTERFACE_RECALIBRATION,OUTPUT_FOLDER,IC_REGION,REF_SENSORS,REF_PROC_VERS, $
                     CAL_SENSORS,CAL_PROC_VERS,CLOUD_PERCENTAGE,ROI_PERCENTAGE,             $
                     VZA_MIN,VZA_MAX,VAA_MIN,VAA_MAX,SZA_MIN,SZA_MAX,SAA_MIN,SAA_MAX,       $
                     ALL=ALL,VERBOSE=VERBOSE

;-----------------------------------------
; CHECK OUTPUT_FOLDER EXISTS 
  
  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_RECAL: STARTING INTERFACE MODULE'
  RES = FILE_INFO(OUTPUT_FOLDER)
  IF RES.EXISTS EQ 0 THEN BEGIN
    PRINT,"DIMITRI_INTERFACE_RECAL: OUTPUT FOLDER DOESN'T EXIST"
    RETURN,-1
  ENDIF

;-----------------------------------------
; CHECKS OFFSET AND PERCENTAGES ARE OK

  CP_LIMIT = FLOAT(CLOUD_PERCENTAGE)*.01
  RP_LIMIT = FLOAT(ROI_PERCENTAGE)*.01

  IF  CP_LIMIT  GT 1.0 OR CP_LIMIT  LT 0.0 OR $
      RP_LIMIT  GT 1.0 OR RP_LIMIT  LT 0.0 THEN BEGIN
    PRINT, 'DIMITRI_INTERFACE_RECAL: ERROR, CLOUD/ROI PERCENTAGES OUT OF RANGE'
    RETURN,-1
  ENDIF

;-----------------------------------------
; CHECK ARRAYS ARE THER SAME SIZE UNLESS 'ALL' 
; KEYWORD SET

  N_REFS = N_ELEMENTS(REF_SENSORS)
  N_REFP = N_ELEMENTS(REF_PROC_VERS)
  N_CALS = N_ELEMENTS(CAL_SENSORS)
  N_CALP = N_ELEMENTS(CAL_PROC_VERS)

  IF NOT KEYWORD_SET(ALL) AND $
    (N_REFS+N_REFP+N_CALS+N_CALP)/4 NE N_REFS THEN BEGIN
    PRINT,'DIMITRI_INTERFACE_RECAL: ERROR, INPUT SENSOR ARRAYS NOT CONSISTENT
    RETURN,-1 
  ENDIF
  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_RECAL: INPUT SENSOR ARRAYS NOMINAL'

;-----------------------------------------
; CREATE NEW ARRAYS TO CONTAIN SENSOR INFORMATION
  
  IF NOT KEYWORD_SET(ALL) THEN BEGIN
    RREFS = REF_SENSORS & RREFP = REF_PROC_VERS & RCALS = CAL_SENSORS & RCALP = CAL_PROC_VERS
  ENDIF ELSE BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_RECAL: ALL COMBINATIONS KEYWORD SET, CREATING NEW ARRAYS'
    N_COM = N_REFS*N_CALS
    RREFS = STRARR(N_COM) & RREFP = STRARR(N_COM) & RCALS = STRARR(N_COM) & RCALP = STRARR(N_COM)
    REF_COUNT=0
    FOR NR=0,N_REFS-1 DO BEGIN
      FOR NC=0,N_CALS-1 DO BEGIN
        RREFS[REF_COUNT] = REF_SENSORS[NR]
        RREFP[REF_COUNT] = REF_PROC_VERS[NR]
        RCALS[REF_COUNT] = CAL_SENSORS[NC]
        RCALP[REF_COUNT] = CAL_PROC_VERS[NC]  
        REF_COUNT++
      ENDFOR
    ENDFOR
  ENDELSE

;-----------------------------------------
; LOOP OVER THE REQUIRED REF_SENSOR-CAL_SENSOR 
; COMBINATIONS FOR RECALIBRATION

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_RECAL: STARTING LOOP ON REQUIRED SENSOR COMBINATIONS'
  FOR DI_RCAL = 0,N_ELEMENTS(RREFS)-1 DO BEGIN
      RES = RECALIBRATE_DOUBLETS(OUTPUT_FOLDER,IC_REGION,RREFS[DI_RCAL],RREFP[DI_RCAL],RCALS[DI_RCAL],$
                                 RCALP[DI_RCAL],CP_LIMIT,RP_LIMIT                                    ,$
                                 VZA_MIN,VZA_MAX,VAA_MIN,VAA_MAX,SZA_MIN,SZA_MAX,SAA_MIN,SAA_MAX     ,$
                                 VERBOSE=VERBOSE)
      IF RES EQ -1 THEN BEGIN
        PRINT,'DIMITRI_INTERFACE_RECAL: FATAL ERROR ENCOUNTERED'
        RETURN,-1
      ENDIF
  ENDFOR

;-----------------------------------------
; CALL FUNCTION TO CONCATENATE ALL DATA INTO A SUPER SENSOR

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_RECAL: STARTING LOOP TO GENERATE SUPERSENSORS'
  FOR DI_RCAL = 0,N_ELEMENTS(RREFS)-1 DO BEGIN
    RES = CONCATENATE_TOA_REFLECTANCE(OUTPUT_FOLDER,IC_REGION,RREFS[DI_RCAL],RREFP[DI_RCAL],VERBOSE=VERBOSE)
  ENDFOR
  
;----------------------------------------
; RETURN NO FATAL ERRORS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DIMITRI_INTERFACE_RECAL: COMPLETED RECALIBRATION, NO FATAL ERRORS IDENTIFIED'  
  RETURN,1

END