;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      DHMI_CS_SETUP     
;* 
;* PURPOSE:
;*      THIS PROGRAM GENERATES AN INTERFACE WIDGET ALLOWING SELECTION OF PARAMETERS FOR 
;*      THE DIMITRI CLOUD SCREENING MODULE. THE WIDGET USES THE USER SELECTED SITE, REGION, 
;*      PROCESSING VERSION AND YEAR AND SEARCHES FOR THE CORRESPONDING FILES WITHIN THE 
;*      DIMITRI DATABASE. THIS INDEX IS THEN PASSED TO THE DIMITRI CLOUD SREENING MODULE 
;*      ALONG WITH THE DATABASE DATA
;*
;* CALLING SEQUENCE:
;*      DHMI_CS_SETUP     
;*
;* INPUTS:
;*      NONE
;*
;* KEYWORDS:
;*      GROUP_LEADER  - ID OF THE WIDGET TO BE USED AS THE GROUP LEADER
;*      VERBOSE       - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      NONE
;*
;* COMMON BLOCKS:
;*      DHMI_DATABASE - CONTAINS THE DATABASE DATA FOR THE DIMITRI HMI
;*
;* MODIFICATION HISTORY:
;*      12 MAR 2011 - C KENT    - DIMITRI-2 V1.0
;*      13 MAR 2011 - C KENT    - ADDED VERBOSE COMMENTS
;*      21 MAR 2011 - C KENT    - MODIFIED FILE DEFINITION TO USE GET_DIMITRI_LOCATION
;*      06 JUL 2011 - C KENT    - ADDED DATABASE COMMON BLOCK TO DIMITRI HMI
;*      30 AUG 2011 - C KENT    - REMOVE 'ALL' OPTION FROM SITE/SENSOR/PROCV
;*
;* VALIDATION HISTORY:
;*      14 APR 2011 - C KENT    - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                                COMPILATION AND OPERATION
;*
;**************************************************************************************
;**************************************************************************************

PRO DHMI_CS_SETUP_EXIT,EVENT

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_CS_SETUP_INFO, /NO_COPY

;--------------------------
; CLEAN UP OBJECTS

  IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->EXIT: DESTROYING FSC OBJECTS'
  OBJ_DESTROY,DHMI_CS_SETUP_INFO.FSCSITE
  OBJ_DESTROY,DHMI_CS_SETUP_INFO.FSCSENS
  OBJ_DESTROY,DHMI_CS_SETUP_INFO.FSCPROC
  OBJ_DESTROY,DHMI_CS_SETUP_INFO.FSCYEAR

;--------------------------
; DESTROY THE WIDGET

  IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->EXIT: DESTORYING THE SETUP WIDGET'
  WIDGET_CONTROL,EVENT.TOP,/DESTROY

END

;**************************************************************************************
;**************************************************************************************

PRO DHMI_CS_SETUP_START,EVENT

COMMON DHMI_DATABASE

;--------------------------
; GET EVENT AND WIDGET INFO

  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_CS_SETUP_INFO, /NO_COPY

;--------------------------
; READ UPDATED DATABASE

  IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->START: RETRIEVING LATEST DATABASE VERSION'

;--------------------------
; GET SITE, SENSOR,PROCV AND 
; YEAR VALUES

  IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->START: RETRIEVING SITE, SENSOR, PROCV AND YEAR VALUES'
  DHMI_CS_SETUP_INFO.FSCSITE->GETPROPERTY,VALUE=SITE
  DHMI_CS_SETUP_INFO.FSCSENS->GETPROPERTY,VALUE=SENSOR
  DHMI_CS_SETUP_INFO.FSCPROC->GETPROPERTY,VALUE=PROCV
  DHMI_CS_SETUP_INFO.FSCYEAR->GETPROPERTY,VALUE=YEAR
  
  CSITE   = SITE
  CSENSOR = SENSOR
  CPROCV  = PROCV
  IF YEAR   EQ 'ALL' THEN CYEAR   ='*' ELSE CYEAR   = YEAR

;--------------------------  
; GET INDEX OF WHERE IN DB_DATA 
; THESE VALUES EXIST

  IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->START: FINDING INDEX OF MATCHING DATABASE RECORDS'
  DB_IDX = WHERE(STRMATCH(DHMI_DB_DATA.REGION,CSITE)              AND $
              STRMATCH(DHMI_DB_DATA.SENSOR,CSENSOR)               AND $
              STRMATCH(DHMI_DB_DATA.PROCESSING_VERSION,CPROCV)    AND $
              STRMATCH(STRTRIM(STRING(DHMI_DB_DATA.YEAR),2),CYEAR)    $
              )

;-------------------------- 
; SEND DATA TO CLOUD SCREENING MODULE

  IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->START: LOADING THE CLOUD SCREENING MODULE'  
  DHMI_CLOUD_SCREENING,DB_IDX,GROUP_LEADER=EVENT.TOP,VERBOSE=VERBOSE
  ;DHMI_CLOUD_SCREENING,DHMI_CS_SETUP_INFO.DB_DATA,DB_IDX,GROUP_LEADER=EVENT.TOP,VERBOSE=VERBOSE

;--------------------------
; RETURN TO THE WIDGET

  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_CS_SETUP_INFO, /NO_COPY

END

;**************************************************************************************
;**************************************************************************************

PRO DHMI_CS_SETUP_CHANGE,EVENT

COMMON DHMI_DATABASE
  WIDGET_CONTROL, EVENT.TOP,  GET_UVALUE=DHMI_CS_SETUP_INFO, /NO_COPY
  WIDGET_CONTROL, EVENT.ID,   GET_UVALUE=ACTION

;--------------------------
; GET THE ACTION TYPE

  ACTION_TYPE = STRMID(ACTION,0,1)

;--------------------------
; UPDATE SENSOR VALUE

  IF ACTION_TYPE EQ 'V' THEN BEGIN
    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE SITE FIELD AND INDEX'
    CASE ACTION OF
      'VSITE<':DHMI_CS_SETUP_INFO.ISITE = DHMI_CS_SETUP_INFO.ISITE-1
      'VSITE>':DHMI_CS_SETUP_INFO.ISITE = DHMI_CS_SETUP_INFO.ISITE+1
    ENDCASE
    IF DHMI_CS_SETUP_INFO.ISITE LT 0 THEN DHMI_CS_SETUP_INFO.ISITE = DHMI_CS_SETUP_INFO.NASITE-1
    IF DHMI_CS_SETUP_INFO.ISITE EQ DHMI_CS_SETUP_INFO.NASITE THEN DHMI_CS_SETUP_INFO.ISITE = 0 
    
    DHMI_CS_SETUP_INFO.FSCSITE->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.ASITE[DHMI_CS_SETUP_INFO.ISITE]

;--------------------------
; GET AVAILABLE SENSORS WITHIN REGION

    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE SENSOR FIELD AND INDEX'
    CSITE=DHMI_CS_SETUP_INFO.ASITE[DHMI_CS_SETUP_INFO.ISITE]
     
    TEMP = DHMI_DB_DATA.SENSOR[WHERE(STRMATCH(DHMI_DB_DATA.REGION,CSITE))]
    TEMP = TEMP[UNIQ(TEMP,SORT(TEMP))]
    DHMI_CS_SETUP_INFO.ASENS[0:N_ELEMENTS(TEMP)-1] = TEMP;,'ALL']
    DHMI_CS_SETUP_INFO.NASENS = N_ELEMENTS(TEMP);+1
    DHMI_CS_SETUP_INFO.ISENS  = 0
    DHMI_CS_SETUP_INFO.FSCSENS->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.ASENS[DHMI_CS_SETUP_INFO.ISENS]  
 
    GOTO,UPDATE_PROC
    
  ENDIF

;--------------------------
; UPDATE SENSOR VALUE

  IF ACTION_TYPE EQ 'S' THEN BEGIN
    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE SENSOR FIELD AND INDEX'
    CASE ACTION OF
      'SENS<':DHMI_CS_SETUP_INFO.ISENS = DHMI_CS_SETUP_INFO.ISENS-1
      'SENS>':DHMI_CS_SETUP_INFO.ISENS = DHMI_CS_SETUP_INFO.ISENS+1
    ENDCASE
    IF DHMI_CS_SETUP_INFO.ISENS LT 0 THEN DHMI_CS_SETUP_INFO.ISENS = DHMI_CS_SETUP_INFO.NASENS-1
    IF DHMI_CS_SETUP_INFO.ISENS EQ DHMI_CS_SETUP_INFO.NASENS THEN DHMI_CS_SETUP_INFO.ISENS = 0 
    
    DHMI_CS_SETUP_INFO.FSCSENS->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.ASENS[DHMI_CS_SETUP_INFO.ISENS]

;--------------------------
; GET AVAILABLE PROC_VERS 
; FOR SITE AND SENSOR
    
    UPDATE_PROC:
    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE PROCESSING VERSION FIELD AND INDEX'
    CSITE = DHMI_CS_SETUP_INFO.ASITE[DHMI_CS_SETUP_INFO.ISITE]
    CSENS = DHMI_CS_SETUP_INFO.ASENS[DHMI_CS_SETUP_INFO.ISENS]   
  
    TEMP = DHMI_DB_DATA.PROCESSING_VERSION[WHERE($
                                                 STRMATCH(DHMI_DB_DATA.REGION,CSITE) AND $
                                                 STRMATCH(DHMI_DB_DATA.SENSOR,CSENS))]
    TEMP = TEMP[UNIQ(TEMP,SORT(TEMP))]
    DHMI_CS_SETUP_INFO.APROC[0:N_ELEMENTS(TEMP)-1] = TEMP;,'ALL']
    DHMI_CS_SETUP_INFO.NAPROC = N_ELEMENTS(TEMP);+1
    DHMI_CS_SETUP_INFO.IPROC  = 0
    DHMI_CS_SETUP_INFO.FSCPROC->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.APROC[DHMI_CS_SETUP_INFO.IPROC]  
 
    GOTO,UPDATE_YEAR
    
  ENDIF

;--------------------------
; UPDATE PROC VALUE

  IF ACTION_TYPE EQ 'P' THEN BEGIN
    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE PROCESSING VERSION FIELD AND INDEX'
    CASE ACTION OF
      'PROC<':DHMI_CS_SETUP_INFO.IPROC = DHMI_CS_SETUP_INFO.IPROC-1
      'PROC>':DHMI_CS_SETUP_INFO.IPROC = DHMI_CS_SETUP_INFO.IPROC+1
    ENDCASE
    IF DHMI_CS_SETUP_INFO.IPROC LT 0 THEN DHMI_CS_SETUP_INFO.IPROC = DHMI_CS_SETUP_INFO.NAPROC-1
    IF DHMI_CS_SETUP_INFO.IPROC EQ DHMI_CS_SETUP_INFO.NAPROC THEN DHMI_CS_SETUP_INFO.IPROC = 0 
    
    DHMI_CS_SETUP_INFO.FSCPROC->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.APROC[DHMI_CS_SETUP_INFO.IPROC]

;--------------------------
; GET AVAILABLE YEARS FOR SITE,
; SENSOR AND PROC VERSION
    
    UPDATE_YEAR:
    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE YEAR FIELD AND INDEX'
    CSITE = DHMI_CS_SETUP_INFO.ASITE[DHMI_CS_SETUP_INFO.ISITE]
    CSENS = DHMI_CS_SETUP_INFO.ASENS[DHMI_CS_SETUP_INFO.ISENS]   
    CPROC = DHMI_CS_SETUP_INFO.APROC[DHMI_CS_SETUP_INFO.IPROC]
 
    TEMP = STRTRIM(STRING(DHMI_DB_DATA.YEAR[WHERE($
                                                  STRMATCH(DHMI_DB_DATA.REGION,CSITE)       AND $
                                                  STRMATCH(DHMI_DB_DATA.SENSOR,CSENS)       AND $
                                                  STRMATCH(DHMI_DB_DATA.PROCESSING_VERSION,CPROC))]),2)
    TEMP = TEMP[UNIQ(TEMP,SORT(TEMP))]
    DHMI_CS_SETUP_INFO.AYEAR[0:N_ELEMENTS(TEMP)] = [TEMP,'ALL']
    DHMI_CS_SETUP_INFO.NAYEAR = N_ELEMENTS(TEMP)+1
    DHMI_CS_SETUP_INFO.IYEAR=0
    DHMI_CS_SETUP_INFO.FSCYEAR->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.AYEAR[DHMI_CS_SETUP_INFO.IYEAR]  
    
  ENDIF

;--------------------------
; UPDATE YEAR VALUE

  IF ACTION_TYPE EQ 'Y' THEN BEGIN
    IF DHMI_CS_SETUP_INFO.IVERBOSE EQ 1 THEN PRINT,'DHMI_CS_SETUP->CHANGE: UPDATING THE YEAR FIELD AND INDEX'
    CASE ACTION OF
      'YEAR<':DHMI_CS_SETUP_INFO.IYEAR = DHMI_CS_SETUP_INFO.IYEAR-1
      'YEAR>':DHMI_CS_SETUP_INFO.IYEAR = DHMI_CS_SETUP_INFO.IYEAR+1
    ENDCASE
    IF DHMI_CS_SETUP_INFO.IYEAR LT 0 THEN DHMI_CS_SETUP_INFO.IYEAR = DHMI_CS_SETUP_INFO.NAYEAR-1
    IF DHMI_CS_SETUP_INFO.IYEAR EQ DHMI_CS_SETUP_INFO.NAYEAR THEN DHMI_CS_SETUP_INFO.IYEAR = 0 
    
    DHMI_CS_SETUP_INFO.FSCYEAR->SETPROPERTY, VALUE=DHMI_CS_SETUP_INFO.AYEAR[DHMI_CS_SETUP_INFO.IYEAR]
  ENDIF

;--------------------------
; RETRUN TO THE WIDGET

  WIDGET_CONTROL, EVENT.TOP, SET_UVALUE=DHMI_CS_SETUP_INFO, /NO_COPY

END

;**************************************************************************************
;**************************************************************************************

PRO DHMI_CS_SETUP,GROUP_LEADER=GROUP_LEADER,VERBOSE=VERBOSE

COMMON DHMI_DATABASE

;;--------------------------
;; FIND MAIN DIMITRI FOLDER AND DELIMITER
;
  IF KEYWORD_SET(VERBOSE) THEN BEGIN
    PRINT,'DHMI_CS_SETUP: STARTING HMI VISUALISATION ROUTINE'
    IVERBOSE=1
  ENDIF ELSE IVERBOSE=0
  IF STRUPCASE(!VERSION.OS_FAMILY) EQ 'WINDOWS' THEN WIN_FLAG = 1 ELSE WIN_FLAG = 0
  DL            =  GET_DIMITRI_LOCATION('DL')

;--------------------------
; GET LIST OF ALL OUTPUT FOLDERS, 
; SITES, SENSORS AND PROCESSING VERSIONS

  ASITES = DHMI_DB_DATA.REGION[UNIQ(DHMI_DB_DATA.REGION,SORT(DHMI_DB_DATA.REGION))];,'ALL']
  USENSS = DHMI_DB_DATA.SENSOR[UNIQ(DHMI_DB_DATA.SENSOR,SORT(DHMI_DB_DATA.SENSOR))]
  UPROCV = DHMI_DB_DATA.PROCESSING_VERSION[UNIQ(DHMI_DB_DATA.PROCESSING_VERSION,$
                                       SORT(DHMI_DB_DATA.PROCESSING_VERSION))]
  UYEARS = DHMI_DB_DATA.YEAR[UNIQ(DHMI_DB_DATA.YEAR,SORT(DHMI_DB_DATA.YEAR))]

;--------------------------  
; SELECT FIRST SITE AND GET 
; AVAILABLE SENSORS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: RETRIEVING AVAILABLE SITES AND SENSORS'
  ASENS = MAKE_ARRAY(N_ELEMENTS(USENSS),/STRING,VALUE='')
  APROC = MAKE_ARRAY(N_ELEMENTS(UPROCV),/STRING,VALUE='')
  AYEAR = MAKE_ARRAY(N_ELEMENTS(UYEARS)+1,/STRING,VALUE='')

  NASITE = N_ELEMENTS(ASITES)
  CSITE  = ASITES[0]
  TEMP   = DHMI_DB_DATA.SENSOR[WHERE(DHMI_DB_DATA.REGION EQ CSITE)]
  TEMP   = TEMP[UNIQ(TEMP,SORT(TEMP))]
  ASENS[0:N_ELEMENTS(TEMP)-1] = TEMP;,'ALL']
  NASENS = N_ELEMENTS(TEMP);+1
  CSENS  = ASENS[0]

;--------------------------  
; GET AVAILABLE PROCESSING VERSIONS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: RETRIEVING AVAILABLE PROCESSING VERSIONS'
  TEMP    = DHMI_DB_DATA.PROCESSING_VERSION[WHERE(DHMI_DB_DATA.REGION EQ CSITE AND $
                                                  DHMI_DB_DATA.SENSOR EQ CSENS)]
  TEMP    = TEMP[UNIQ(TEMP,SORT(TEMP))]
  APROC[0:N_ELEMENTS(TEMP)-1] = TEMP;,'ALL']
  NAPROC  = N_ELEMENTS(TEMP);+1
  CPROC   = APROC[0]

;--------------------------  
; GET AVAILABLE YEARS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: RETRIEVING AVAILABLE YEARS'  
  TEMP    = STRTRIM(STRING(DHMI_DB_DATA.YEAR[WHERE(DHMI_DB_DATA.REGION EQ CSITE AND $
                                              DHMI_DB_DATA.SENSOR EQ CSENS AND $
                                              DHMI_DB_DATA.PROCESSING_VERSION EQ CPROC)]),2)
  TEMP    = TEMP[UNIQ(TEMP,SORT(TEMP))]
  AYEAR[0:N_ELEMENTS(TEMP)] = [TEMP,'ALL']
  CYEAR   = AYEAR[0]
  NAYEAR  = N_ELEMENTS(TEMP)+1

;--------------------------
; DEFINE THE MAIN WIDGET INCLUDING 
; FSC_FIELDS AND DROPLIST

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: RETRIEVING SCREEN DIMENSIONS FOR WIDGET'
  DIMS  = GET_SCREEN_SIZE()
  OPT_BTN = 60
  IF WIN_FLAG THEN XSIZE = 350 ELSE XSIZE = 380
  YSIZE = 500
  XLOC  = (DIMS[0]/2)-(XSIZE/2)
  YLOC  = (DIMS[1]/2)-(YSIZE/2)

  DHMI_CS_SETUP_TLB = WIDGET_BASE(COLUMN=1,TITLE='DIMITRI V2.0: CLOUD SCREENING SETUP',XSIZE=XSIZE,$
                                  XOFFSET=XLOC,YOFFSET=YLOC)

  DHMI_CS_SETUP_VAL     = WIDGET_BASE(DHMI_CS_SETUP_TLB,ROW=5,FRAME=1)
  DHMI_CS_SETUP_VAL_LBL = WIDGET_LABEL(DHMI_CS_SETUP_VAL,VALUE='Cloud Screening Parameters:')
  DHMI_CS_SETUP_VAL_LBL = WIDGET_LABEL(DHMI_CS_SETUP_VAL,VALUE='')
  DHMI_CS_SETUP_VAL_LBL = WIDGET_LABEL(DHMI_CS_SETUP_VAL,VALUE='')

;--------------------------
; DEFINE WIDGET FSC FIELDS AND BUTTONS

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: DEFINING THE FSC_FIELDS AND BUTTONS'
  IF WIN_FLAG THEN DHMI_CS_SETUP_VAL_VID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Region        : ', VALUE=CSITE,OBJECT=FSCSITE,/NOEDIT) $
    ELSE DHMI_CS_SETUP_VAL_VID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Region     : ', VALUE=CSITE,OBJECT=FSCSITE,/NOEDIT)
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='<',UVALUE='VSITE<',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='>',UVALUE='VSITE>',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')

  IF WIN_FLAG THEN DHMI_CS_SETUP_VAL_SID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Sensor        : ', VALUE=CSENS,OBJECT=FSCSENS,/NOEDIT) $
    ELSE DHMI_CS_SETUP_VAL_SID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Sensor     : ', VALUE=CSENS,OBJECT=FSCSENS,/NOEDIT)
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='<',UVALUE='SENS<',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='>',UVALUE='SENS>',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')

  IF WIN_FLAG THEN DHMI_CS_SETUP_VAL_PID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Processing  : ',VALUE=CPROC,OBJECT=FSCPROC,/NOEDIT) $
    ELSE DHMI_CS_SETUP_VAL_PID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Processing : ',VALUE=CPROC,OBJECT=FSCPROC,/NOEDIT)
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='<',UVALUE='PROC<',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='>',UVALUE='PROC>',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')
  
  IF WIN_FLAG THEN DHMI_CS_SETUP_VAL_YID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Year            : ',VALUE=CYEAR,OBJECT=FSCYEAR,/NOEDIT) $
    ELSE DHMI_CS_SETUP_VAL_YID  = FSC_FIELD(DHMI_CS_SETUP_VAL,TITLE='Year       : ',VALUE=CYEAR,OBJECT=FSCYEAR,/NOEDIT)
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='<',UVALUE='YEAR<',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')
  DHMI_CS_SETUP_VAL_TMP  = WIDGET_BUTTON(DHMI_CS_SETUP_VAL,VALUE='>',UVALUE='YEAR>',$
                                          EVENT_PRO='DHMI_CS_SETUP_CHANGE')

;--------------------------
; DEFINE WIDGET BASE IN MAIN

  DHMI_CS_SETUP_TLB_OPT  = WIDGET_BASE(DHMI_CS_SETUP_TLB,ROW=1,/ALIGN_RIGHT)

;--------------------------
; DEFINE TWO BUTTONS FOR START AND CLOSE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: DEFINING THE START AND EXCIT BUTTONS'
  DHMI_CS_SETUP_TLB_OPT_BTN  = WIDGET_BUTTON(DHMI_CS_SETUP_TLB_OPT,VALUE='Start',UVALUE='START',$
                                              EVENT_PRO='DHMI_CS_SETUP_START',XSIZE=OPT_BTN)
  DHMI_CS_SETUP_TLB_OPT_BTN  = WIDGET_BUTTON(DHMI_CS_SETUP_TLB_OPT,VALUE='Close',UVALUE='EXIT',$
                                              EVENT_PRO='DHMI_CS_SETUP_EXIT',XSIZE=OPT_BTN)

  IF NOT KEYWORD_SET(GROUP_LEADER) THEN GROUP_LEADER = DHMI_CS_SETUP_TLB

;--------------------------
; STORE WIDGET INFORMATION IN A STRUCTURE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: STORING WIDGET INFO IN A STRUCTURE'
  DHMI_CS_SETUP_INFO = {                            $
                        IVERBOSE    : IVERBOSE      ,$
                        GROUP_LEADER: GROUP_LEADER  ,$ 
                        ASITE       : ASITES        ,$
                        NASITE      : NASITE        ,$
                        ASENS       : ASENS         ,$
                        NASENS      : NASENS        ,$
                        APROC       : APROC         ,$
                        NAPROC      : NAPROC        ,$
                        AYEAR       : AYEAR         ,$
                        NAYEAR      : NAYEAR        ,$
                        FSCSITE     : FSCSITE       ,$
                        FSCSENS     : FSCSENS       ,$
                        FSCPROC     : FSCPROC       ,$
                        FSCYEAR     : FSCYEAR       ,$
                        ISITE       : 0             ,$
                        ISENS       : 0             ,$
                        IPROC       : 0             ,$
                        IYEAR       : 0             $
                        }

;--------------------------
; REALISE THE WIDGET AND 
; REGISTER WITH THE XMANAGER

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'DHMI_CS_SETUP: REALISING THE WIDGET AND REGISTERING WITH XMANAGER'
  WIDGET_CONTROL,DHMI_CS_SETUP_TLB,/REALIZE,/NO_COPY,SET_UVALUE=DHMI_CS_SETUP_INFO,GROUP_LEADER=GROUP_LEADER
  XMANAGER,'DHMI_CS_SETUP', DHMI_CS_SETUP_TLB

END