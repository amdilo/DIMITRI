;**************************************************************************************
;**************************************************************************************
;*
;* NAME:
;*      ROUJEAN_BRDF_KERNEL_F2       
;* 
;* PURPOSE:
;*      RETURNS THE 2ND ROUJEAN BRDF COEFICIENT
;* 
;* CALLING SEQUENCE:
;*      RES = ROUJEAN_BRDF_KERNEL_F2(K2_SZA,K2_VZA,K2_RAA)      
;* 
;* INPUTS:
;*      K2_SZA    - A SCALAR OR ARRAY OF SOLAR ZENITH VALUES IN RADIANS FOR COMPUTATION
;*      K2_VZA    - A SCALAR OR ARRAY OF VIEWING ZENITH VALUES IN RADIANS FOR COMPUTATION
;*      K2_RAA    - A SCALAR OR ARRAY OF RELATIVE AZIMUTH VALUES IN RADIANS FOR COMPUTATION
;*
;* KEYWORDS:
;*      DEGREES   - SET TO INDICATE INPUT ANGLES ARE DEGREES AND NOT RADIANS AS EXPECTED      
;*      VERBOSE   - PROCESSING STATUS OUTPUTS
;*
;* OUTPUTS:
;*      KERNEL_F2 - A FLOAT OF 2ND ROUJEAN COEFICIENT
;*
;* COMMON BLOCKS:
;*      NONE
;*
;* MODIFICATION HISTORY:
;*      22 MAR 2007 - M BOUVET - PROTOTYPE DIMITRI VERSION
;*      24 JAN 2011 - C KENT   - DIMITRI-2 V1.0
;*
;* VALIDATION HISTORY:
;*      14 APR 2011 - C KENT   - WINDOWS 32-BIT IDL 7.1 AND LINUX 64-BIT IDL 8.0 NOMINAL
;*                               COMPILATION AND OPERATION. TESTED ON MERIS 2ND REPROCESSING 
;*                               WITH MERIS 3RD REPROCESSING AND MODISA COLLECTION 5
;*
;**************************************************************************************
;**************************************************************************************

FUNCTION ROUJEAN_BRDF_KERNEL_F2,K2_SZA,K2_VZA,K2_RAA,DEGREES=DEGREES,VERBOSE=VERBOSE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'ROUJEAN_BRDF_KERNEL_F2: STARTING ROUJEAN COMPUTATION'

;-----------------------------------------
; TURN INPUT ANGLES INTO RADIANS IF NEEDED

  IF KEYWORD_SET(DEGREES) THEN BEGIN
    IF KEYWORD_SET(VERBOSE) THEN PRINT,'ROUJEAN_BRDF_KERNEL_F2: DEGREES KEYWORD SET'
    SZA_RD = K2_SZA*!DTOR
    VZA_RD = K2_VZA*!DTOR
    TEMP   = WHERE(ABS(K2_RAA) GT 180.0)
    IF TEMP[0] GT -1 THEN K2_RAA[TEMP] = 360.0-K2_RAA[TEMP]
    RAA_RD = K2_RAA*!DTOR
  ENDIF ELSE BEGIN
    SZA_RD = K2_SZA
    VZA_RD = K2_VZA 
    TEMP   = WHERE(ABS(K2_RAA) GT 2*(!DPI))
    IF TEMP[0] GT -1 THEN K2_RAA[TEMP] = 2*(!DPI)-K2_RAA[TEMP]
    RAA_RD = K2_RAA
  ENDELSE

;-----------------------------------------
; COMPUTE THE ROUJEAN KERNEL F2 VALUE

  SCATT_ANGLE_RD  = ACOS(COS(SZA_RD)*COS(VZA_RD)+SIN(SZA_RD)*SIN(VZA_RD)*COS(RAA_RD))
  KERNEL_F2       = 4./3./!DPI/(COS(SZA_RD)+COS(VZA_RD))
  KERNEL_F2       = KERNEL_F2*((!DPI/2.-SCATT_ANGLE_RD)*COS(SCATT_ANGLE_RD)+SIN(SCATT_ANGLE_RD))
  KERNEL_F2       = KERNEL_F2-1./3.

;-----------------------------------------
;RETURN THE KERNEL VALUE

  IF KEYWORD_SET(VERBOSE) THEN PRINT,'ROUJEAN_BRDF_KERNEL_F2: RETURNING THE ROUJEAN F2 VALUE'  
  RETURN, KERNEL_F2

END